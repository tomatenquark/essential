// Extra menus created by the community

// demo editor, advanced ent gui, prefabs gui and hud options by Salatiel#5274 (tomatenquark.github.io)

//demo editor -- start
demoeditor = [
	showgui demomanage
]
//valid numbers from 0-60
dmm_valids = 0
loop validn 60 [
	dmm_valids = (concatword $dmm_valids $validn)
]
//remove special characters
dmm_fixstr = [
	$arg1 = (strreplace $dmm_projectname "^"" \"") //don't ask me why
	$arg1 = (strreplace $dmm_projectname " " "-")
	$arg1 = (strreplace $dmm_projectname "^n" "")
	$arg1 = (strreplace $dmm_projectname "^t" "")
]

if (=s (getalias dmm_minutes) "")[dmm_minutes = 9]
if (=s (getalias dmm_seconds) "")[dmm_seconds = 35]
if (=s (getalias dmm_demoname) "")[dmm_demoname = ""]

dmm_goto = [
	gamespeed 1000
	if (!= $arg1 "")[dmm_goingto = $arg1]
	if (> $dmm_goingto $timeremaining)[
		if (=s $dmm_demoname "")[
			echo "^f7You can't travel backwards without setting the demo name."
			clearsleep
			gamespeed 100
			pausegame 1
		] [
			demo $dmm_demoname
			clearsleep
			sleep 1 [dmm_goto]
		]
	] [
		if (!= $timeremaining $dmm_goingto)[
			sleep 1 [dmm_goto]
		] [
			pausegame 1
			echo "^f7Done, game paused."
			gamespeed 100
		]
	]
]

dmm_showlist = 1
dmm_gettimeline = [
	dmm_ct = 0
	dmm_ct2 = 0
	guitab "^f7Timeline"
	guititle "";
	guiitemmove -1 0 -1 [guitext "^f4[-----------------------------------]" 0]
	looplist curcmd $(concatword $dmm_projectname "_dmm_actionslistCMD") [
		curtime = (at $(concatword $dmm_projectname "_dmm_actionslistTIME") $dmm_ct)
		if (> $curtime $timeremaining)[dmm_statecolor = "^f0"]
		if (< $curtime $timeremaining)[dmm_statecolor = "^f4"]
		if (= $curtime $timeremaining)[dmm_statecolor = "^f6"]
		guiitemmove -1 (divf $curtime 30) -1 [
			guibutton (concatword  $dmm_statecolor "|") [echo (format "^f7[^f0%1^f7]: ^f2%2:%3" @curcmd (div $curtime 60) (mod $curtime 60))] 0
		]
		dmm_ct = (+ $dmm_ct 1)
	]
	guiitemmove -1 (-f (divf $timeremaining 30) 0.95) -1 [guitext "^f5<]" 0]
	guiitemmove -1 (-f (divf $timeremaining 30) 1.5) -1.7 [guititle (concatword "^f5" (timeremaining 1))]
	
	guialign 0 [
		if (!= $dmm_actionrun 1)[
			if (> (strlen (getalias (concatword $dmm_projectname "_dmm_actionslistCMD"))) 0)[
				guibutton "Run actions" [clearsleep; dmm_actionexec; pausegame 0] checkbox_off
			] [
				guibutton "^f4Run actions" [echo "^f7Add some actions."]
			]
		] [
			guibutton "^f2Stop actions" [dmm_actionrun = 0; clearsleep; pausegame 1] checkbox_on;
		]
	]
	if (&& (= $paused 0) (= $dmm_actionrun 0))[
		guitextbox "the game is running but actions are disabled..." 20
	]
	if (&& (= $paused 1) (= $dmm_actionrun 1))[
		guitextbox "the actions are enabled but the game is paused..." 20
	]
	guibar
	guialign 1 [guibitfield "^f7Show list" dmm_showlist 1]
	if (dmm_showlist)[
		looplist curcmd $(concatword $dmm_projectname "_dmm_actionslistCMD") [
			curtime = (at $(concatword $dmm_projectname "_dmm_actionslistTIME") $dmm_ct2)
			if (> $curtime $timeremaining)[dmm_statecolor = "^f0"]
			if (< $curtime $timeremaining)[dmm_statecolor = "^f4"]
			if (= $curtime $timeremaining)[dmm_statecolor = "^f6"]
			guialign 1 [
				guitext (format "^f7[%1%2^f7] ^f1%3:%4" $dmm_statecolor $curcmd (div $curtime 60) (mod $curtime 60)) 0
				guibar
				guibutton "" [
					(concatword $dmm_projectname "_dmm_actionslistTIME") = (listsplice $(concatword $dmm_projectname "_dmm_actionslistTIME") "" @dmm_ct2 1)
					(concatword $dmm_projectname "_dmm_actionslistCMD") = (listsplice $(concatword $dmm_projectname "_dmm_actionslistCMD") "" @dmm_ct2 1)
				] exit
			]
			dmm_ct2 = (+ $dmm_ct2 1)
		]
	]
]

dmm_actionexec = [
	if (=s $dmm_projectname "")[echo "^f7Insert a project name first."] [
		dmm_actionrun = 1
		loop cmd (listlen $(concatword $dmm_projectname "_dmm_actionslistCMD")) [	
			if (= $cmd (- (listlen $(concatword $dmm_projectname "_dmm_actionslistCMD")) 1))[
				sleep 1000 [dmm_actionexec]
			]
			if (= $timeremaining (at $(concatword $dmm_projectname "_dmm_actionslistTIME") $cmd))[
				dmm_lastusedframe = $timeremaining
				do (at $(concatword $dmm_projectname "_dmm_actionslistCMD") $cmd)
			]
		]
	]
]
dmm_actionrun = 0
if (=s (getalias mymovie_dmm_actionslistCMD) "") [mymovie_dmm_actionslistCMD = ""]
if (=s (getalias mymovie_dmm_actionslistTIME) "") [mymovie_dmm_actionslistTIME = ""]

if (=s (getalias demomanage_actions) "") [demomanage_actions = ""];
if (=s (getalias dmm_projectname) "")[
	dmm_projectname = "mymovie";
]
if (=s (getalias (concatword $dmm_projectname "_dmm_actionslistCMD")) "")[
	(concatword $dmm_projectname "_dmm_actionslistCMD") = ""
]

dmm_addactionlist = [
	if (=s $dmm_projectname "")[echo "^f7Insert a project name first."] [
		(concatword $dmm_projectname "_dmm_actionslistCMD") = (concatword $(concatword $dmm_projectname "_dmm_actionslistCMD") " ^"" $arg1 "^"")
		(concatword $dmm_projectname "_dmm_actionslistTIME") = (concatword $(concatword $dmm_projectname "_dmm_actionslistTIME") " ^"" $arg2 "^"")

		echo (concatword "^f7new action set to ^f0" $dmm_projectname " ^f7in ^f0" $dmm_minutes ":" $dmm_seconds "^n^f7>" $(concatword $dmm_projectname "_dmm_actionslistCMD"))
		//echo $(concatword $dmm_projectname "_dmm_actionslistTIME")
	]
]

newgui demomanage [
	guilist [guistrut 25]
	guistayopen [
		if (|| (= $timeremaining 0) (= $isconnected 0))[
			clearsleep;
			guitext "timeline unavailable^nin this mode"
		] [
			guititle (concatword "^f0" (timeremaining 1))
		]
		guititle ""
		guialign 0 [
			guifield dmm_minutes -5 [
				if (> $dmm_minutes 9)[dmm_minutes = 9]
				if (|| (< $dmm_minutes 0) (=s $dmm_minutes ""))[dmm_minutes = 0]
				if (= (strstr 0123456789 $dmm_minutes) -1)[echo "^f7Only numbers from 0 to 9 are allowed here."; dmm_minutes = 0]
			]
			guibar
			guifield dmm_seconds -5 [
				if (> $dmm_seconds 59)[dmm_seconds = 0 ; if (< $dmm_minutes 9)[dmm_minutes = (+ $dmm_minutes 1)][dmm_minutes = 9 ; dmm_seconds = 59]]
				if (|| (< $dmm_seconds 0) (=s $dmm_seconds ""))[dmm_seconds = 0]
				if (= (strstr $dmm_valids $dmm_seconds) -1)[echo "^f7Only numbers from 0 to 60 are allowed here."; dmm_seconds = 0]
			]
		]
		guialign 0 [guibutton (concatword "jump to " $dmm_minutes ":" $dmm_seconds) [pausegame 0; dmm_goto (+ (* $dmm_minutes 60) $dmm_seconds)] 0]
		guibar
		guialign -1 [guitext "Project name^f7* " 0]
		guifield dmm_projectname -15 [
			dmm_fixstr dmm_projectname
			if (> (strlen $dmm_projectname) 0)[
				if (=s (getalias (concatword $dmm_projectname "_dmm_actionslistCMD")) "")[
					echo (concat "^f7A new project has been created:^f0" $dmm_projectname)
					(concatword $dmm_projectname "_dmm_actionslistCMD") = "";
					(concatword $dmm_projectname "_dmm_actionslistTIME") = "";
				] [
					echo (concat "^f7A previous project was loaded:^f0" $dmm_projectname)
				]
			]
		]
		guialign -1 [guitext "Demo name^f7* " 0]
		guifield dmm_demoname -15 []

		guialign -1 [
			if (> (strlen (getalias (concatword $dmm_projectname "_dmm_actionslistCMD"))) 0)[
				guibutton "^f7clear all actions" [
					echo (concatword "^f7" (listlen $(concatword $dmm_projectname "_dmm_actionslistCMD")) " action(s) have been cleared from the project: ^f0" $dmm_projectname)
					(concatword $dmm_projectname "_dmm_actionslistCMD") = []
					(concatword $dmm_projectname "_dmm_actionslistTIME") = []
				] exit
			] [
				guibutton "^f4clear all actions" [echo "^f7There is no actions to clear."] exit
			]
			guibar
			guibutton "^f7Save/Load" [showgui dmm_share] arrow_fw
		]
		guialign -1 [guitext "Action: " 0]
		guialign 0 [guieditor $demomanage_actions 20 5]
		guialign 0 [
			guibutton (concatword "^f0> ^f7add action to " $dmm_minutes ":" $dmm_seconds)[
				textfocus $demomanage_actions;
				if (=s $textshow "")[echo "^f7Missing action."] [
					dmm_addactionlist $textshow (+ (* $dmm_minutes 60) $dmm_seconds)
				]
			] 0
		]
		
		if (> (strlen $dmm_projectname) 0)[
			if (> (listlen $(concatword $dmm_projectname "_dmm_actionslistCMD")) 0)[
				if (|| (= $timeremaining 0) (= $isconnected 0))[][
					dmm_gettimeline
				]
			]
		]
	]
] "^f7Demo Editor"

newgui dmm_share [
	if (=s (getalias dmm_share_name) "")[dmm_share_name = (concatword $dmm_projectname ".cfg")]
	guistayopen [
		guialign -1 [
			guititle "^f7File: "
			guifield dmm_share_name -15 [
				if (> (strlen $dmm_share_name) 0)[if (= (strstr $dmm_share_name ".") -1)[dmm_share_name = (concatword $dmm_share_name ".cfg")]]
			]
		]
		guialign -1 [
			guibutton "^f0> ^f7Import actions" [
				if (= (strstr $dmm_share_name "/") -1)[
					exec (concatword "demoeditor/" $dmm_share_name)
				] [exec $dmm_share_name]
			] 0
		]
		guialign -1 [
			guibutton "^f0> ^f7Export actions" [
				textfocus dmm_share_project
				if (=s $textshow "")[
					echo "^f7Could not save your actions, try again"
				] [
					textsave (concatword "demoeditor/" $dmm_share_name)
					echo (concatword "^f7Project saved in demoeditor/" $dmm_share_name)
				]
			] 0
		]
		guialign -1 [
			guibutton "^f0> ^f7Send to maptitle" [
				textfocus dmm_share_project
				maptitle $textshow
			] 0
		]
		guialign -1 [guibutton "^f7back" [showgui demomanage] arrow_bw]
		dmm_share_cmd = $(concatword $dmm_projectname "_dmm_actionslistCMD")
		dmm_share_time = $(concatword $dmm_projectname "_dmm_actionslistTIME")
		dmm_share_project = (concatword "echo [^f7new project imported: ^f0" $dmm_projectname " ^f7with " (listlen $dmm_share_cmd) " actions.]^n" (concatword $dmm_projectname "_dmm_actionslistCMD") " = [" $dmm_share_cmd "]^n" (concatword $dmm_projectname "_dmm_actionslistTIME") " = [" $dmm_share_time "]")
		guiitemmove -1 -500 0 [guifield dmm_share_project 1]
	]
] "^f7Import & Export"

//demo editor -- end


// advanced ent gui -- start
//Variables
n = 0
_egcolorprefab = 0
loop t 7 [(concatword "_egentattr_" $t) = 0; loop c 3 [(concatword "_genentcolorslider_" $t "_" $c) = 0]]
_entpemmit1 = [0 1 2 3 4 5]

// Functions
// guiitemmove = [guialign $arg1 [guistrut @arg2; guilist [guistrut @arg3; @arg4]]]
// _guislidertext "label" var min max [action]
_guislidertext = [guitext $arg1; guislider $arg2 $arg3 $arg4 [(@arg5)]]
// _guislidertextinline = [guislider $arg1 $arg2 $arg3 [@arg4]; guiitemmove -1 -1 -1 [(guitext @arg5 0)]]
_entselupdate = [entset (at $_entcur 0) $_egentattr_0 $_egentattr_1 $_egentattr_2 $_egentattr_3 $_egentattr_4 $_egentattr_5]
_genenteditbutton = [guibutton "edit^t^t" [selentedit]]
_genentselbutton = [if (> $enthavesel 1)[guibutton "nearest" [entcancel; nearestent; entautoview; genentgui (entget) 0]] [guibutton (concat "find" (at $_entcur 0)) [entfind (at $_entcur 0); genentgui $_entcur 0]]]
_genentselsnapbutton = [guibitfield "entsnap^t" entselsnap 1]
_genentspmsg = [guitext (concatword (at $_entcur 0) $arg1 "s are only functional in SP modes")]

// _genentsliders ["label1" "label2" "label3"] ["min max" "min max" "min max"] [optional action]
_genentsliders = [
	looplist _attrs $arg1 [
		if (!= $_attrs -1)[
			_slidervalues = (at $arg2 (indexof $arg1 $_attrs))
			_guislidertext $_attrs (concatword "_egentattr_" (indexof $arg1 $_attrs)) (at $_slidervalues 0) (at $_slidervalues 1) [_entselupdate]
		]
	]

	//adjusts ent functions
	if (=s (at $arg1 0) "direction")[
		guialign -1 [(_genentrotatebutton); guibar; (_genentresetbutton (listlen $arg2) [])]; @arg3
		guialign -1 [_genenteditbutton; guibar; _genentselbutton]
		_genentselsnapbutton
	] [
		if (!= $arg3 -1)[
			//line 1 layout
			//if arg3:
			//	<optional action> | reset
			//else:
			//	edit | reset
			guialign -1 [
				if (!=s @arg3 "")[@arg3][_genenteditbutton]
				guibar; (_genentresetbutton (listlen $arg2) [])
			]
			//line 2 layout
			//if arg3:
			//	edit | find similar
			//	entselsnap
			//else: 
			//	entselsnap | find similar
			if (=s @arg3 "")[
				guialign -1 [_genentselsnapbutton; guibar; _genentselbutton]
			] [
				guialign -1 [_genenteditbutton; guibar; _genentselbutton]
				_genentselsnapbutton
			]
		]
	]
]

//0 neutral - 1 good - 2 evil
_genentteams = [if (!= $arg1 1)[guiradio "^f4neutral " _egentattr_1 0 [_entselupdate]]; guiradio "^f1good " _egentattr_1 1 [_entselupdate]; guiradio "^f3evil" _egentattr_1 2 [_entselupdate]]

// _genentresetbutton <args> [action]
_genentresetbutton = [guibutton "reset" [loop c @arg1 [(concatword "_egentattr_" $c) = 0]; _entselupdate; @arg2] "inexor/exit"]

// _entinsel "ent name" [action] "ent name 2"
_entinsel = [if (=s $arg3 "")[arg3 = "none"]; if (&& $editing (|| (=s (at (entget) 0) $arg1) (=s (at (entget) 0) $arg3)))[@arg2]]

//rotates to the nearest multiple of 90
_genentrotatebutton = [
	guibutton "rotate^t" [
		if (= (at $_entcur 1) 360)[_egentattr_0 = 0]
		if (&& (> (at $_entcur 1) 270) (< (at $_entcur 1) 360)) [_egentattr_0 = 360][
			_egentattr_0 = (+ $_egentattr_0 10)
			_egentattr_0 = (+ $_egentattr_0 (mod (- 90 (mod $_egentattr_0 90)) 90))
		];
		entset (at $_entcur 0) $_egentattr_0 (at $_entcur 2) (at $_entcur 3) (at $_entcur 4) *
	]
]

//0xRGB ugly workaround, but readable
_genentcolorslider = [
	_gc16 = [0 1 2 3 4 5 6 7 8 9 a b c d e f]
	_gclbl = [[3R] [0G] [1B]]
	if (!=s $arg2 "")[guitext $arg2][guitext "color"]
	looplist c $_gclbl [guilabelslider (concatword "_genentcolorslider_" $arg1 "_" (indexof $_gclbl $c)) 0 15 [] $c]
	(concatword "_egentattr_" $arg1) = (concatword "0x" (at $_gc16 $(concatword "_genentcolorslider_" $arg1 "_0")) (at $_gc16 $(concatword "_genentcolorslider_" $arg1 "_1")) (at $_gc16 $(concatword "_genentcolorslider_" $arg1 "_2")))
	_entselupdate
]
_allcomplexents = ["barrel" "base" "box" "elevator" "envmap" "flag" "jumppad" "light" "mapmodel" "monster" "particles" "platform" "playerstart" "sound" "spotlight" "teledest" "teleport"]
_allsimpleents = ["shells" "bullets" "rockets" "riflerounds" "grenades" "cartridges" "health" "healthboost" "greenarmour" "yellowarmour" "quaddamage" "carrot" "respawnpoint"]
_genent = [
	guistrut 38 1

	//make sure all gui values are synchronized with the currently selected ent attributes
	loop e 6 [if (|| (!= (at $_entcur (+ $e 1)) $(concatword "_egentattr_" $e)) (= (at $_entcur (+ $e 1)) 0)) [_egentattr_@e = (at $_entcur (+ $e 1))]]
	
	if (&& (!=s $_entguiname "materials") (!= (strstr $_allsimpleents (at $_entcur 0)) -1)) [
		guitext "type"
		guislider _entsimp0 0 (- (listlen $_allsimpleents) 1) [entset (at $_allsimpleents $_entsimp0); genentgui $_entcur 0]
		guialign -1 [_genentselsnapbutton; guibar; _genentselbutton]	
	]

	//Barrel
	_entinsel "barrel" [_genentspmsg; _genentsliders ["direction" "model" "push" "health"] ["0 360" [0 @@nummapmodels] "-100 100" "0 500"]]

	//Base
	_entinsel "base" [
		guitext "bases are only functional in capture modes"
		_genentsliders ["regen ammo" "tag"] ["0 6" "0 20"]
		guitext (concat "type^f0" (at ["random" "shells" "bullets" "rockets" "riflerounds" "grenades" "cartridges"] $_egentattr_0))
	]

	//Box
	_entinsel "box" [_genentspmsg "e"; _genentsliders ["direction" "model" "push"] ["0 360" [0 @@nummapmodels] "-100 100"]]

	//Envmap
	_entinsel "envmap" [
		guialign 1 [guibutton "recalc" [recalc]]
		_genentsliders ["radius"] ["0 500"]
	]

	//Flag
	_entinsel "flag" [
		guialign 1 [_genentteams 1]
		guitext "flags are only functional in CTF modes"
		_genentsliders ["direction"] ["0 360"]
	]

	//Jumppad
	_entinsel "jumppad" [_genentsliders ["z" "y" "x"] ["-127 127" "-127 127" "-127 127"] [guibitfield "soundless^t" _egentattr_3 "-1 0" [_entselupdate]]]

	//Light
	_entinsel "light" [
		guialign 1 [
			guicolor (+ (* (+ (* $_egentattr_1 256) $_egentattr_2) 256) $_egentattr_3)
			guibutton "+" [
				_egcolorprefab = (+ $_egcolorprefab 1); if (> $_egcolorprefab 10)[_egcolorprefab = 0]
				loop e 3 [(concatword "_egentattr_" (+ $e 1)) = (at (at [[255 255 255][192 192 255][255 192 192][192 255 192][255 255 192][255 192 255][192 255 255][255 255 0][0 255 255][255 0 255]] $_egcolorprefab) $e)]
				_entselupdate
			] 0
		]
		guialign 1 [guibutton "calclight" [calclight]; guibar; guibutton "clearlight" [clearlightmaps]]
		_genentsliders ["radius" [3red] [0green] [1blue]] ["0 500" "0 255" "0 255" "0 255"] [guibutton "sunlight^t" [_egentattr_0 = 0; entset (at $_entcur 0) 0 (at $_entcur 2) (at $_entcur 3) (at $_entcur 4) *]]
	]

	//Mapmodel
	_entinsel "mapmodel" [
		_genentsliders ["direction" "model" "trigger"] ["0 360" [0 @@nummapmodels] "0 14"] //use number of registered mapmodels as max value
		if (> $_egentattr_2 0)[
			guibar
			guitext (concat "type^f0" (at [
				"animation loop" "once (popback)" "once (popback & rumble)"
				"once" "once (rumble)" "multiple (popback)" "multiple (popback & rumble)"
				"multiple" "multiple (rumble)" "door (open once)" "door (auto close)"
				"door (locked)" "vanishing trigger" "vanishing trigger (rumble)"
				"End Of Level Trigger"] $_egentattr_2))
			guitext "triggers are only functional in SP modes"
		]
	]

	//Monster
	_entinsel "monster" [
		_genentspmsg
		guitext (concat "type^f0" (at ["ogro / fireball" "rhino / chaingun" "ratamahatta / shotgun" "slith / rifle" "bauul / RL" "hellpig / bite" "knight / iceball" "goblin / slimeball" "spider / grenade"] $_egentattr_1))
		_genentsliders ["direction" "monster" "tag"] ["0 360" "0 8" "0 20"]
	]

	//Particles
	_entinsel "particles" [
		_eparticlesinterval = [0 1 2 3 4 5 6 7 9 10 11 12 13 32 33 34 35]
		if (|| (= $_egentattr_0 8) (&& (> $_egentattr_0 13) (< $_egentattr_0 32)) (> $_egentattr_0 35) (< $_egentattr_0 0)) [
			guitext (concatword "type ^f0particles " $_egentattr_0 "?")
		] [
			guitext (concat "type^f0" (at [
				"fire with smoke" "steam vent" "water fountain"
				"fire ball" "tape" "meter" "metervs" "lightning"
				"steam" "water" "flame" "smoke plume" "snow"
				"lens flares / plain" "lens flares / sparkle"
				"lens flares / sun" "lens flares / sparklesun"
			] (indexof $_eparticlesinterval $_egentattr_0)))
		]
		guilistslider _egentattr_0 $_eparticlesinterval [_entselupdate]

		_entpemmit1 = [0 1 2 3 4 5]
		loop g 22 [_entpemmit1 = (concat $_entpemmit1 (+ 256 $g))]
		_entpemmit2 = [
			@@(concat $_entpemmit1 [
				278 279 280 281 282 283 291 292
				293 301 302 303 304 305 306 307
				308 309 310 311 312 313 314
			])
		]

		//attrs: <dir> <deccolor>
		if (|| (= $_egentattr_0 1) (= $_egentattr_0 2))[
			guitext "direction"
			guislider _egentattr_1 0 5 [_entselupdate]
			if (= $_egentattr_0 2) [_genentcolorslider 2]
		]

		//attrs: <radius> <hexcolor>
		if (= $_egentattr_0 3)[
			guitext "radius"
			guislider _egentattr_1 0 256 [_entselupdate]
			_genentcolorslider 2
		]

		//attrs: <percent> <hexcolor> <hexcolor2>
		if (|| (= $_egentattr_0 5) (= $_egentattr_0 6))[
			guitext "percent"
			guislider _egentattr_1 0 100 [_entselupdate] 
			_genentcolorslider 2
			if (= $_egentattr_0 6)[_genentcolorslider 3 "color 2"]
		]

		//attrs: <R> <G> <B>
		if (&& (>= $_egentattr_0 32) (<= $_egentattr_0 35))[
			_genentsliders [-1 [3red] [0green] [1blue]] [-1 "0 255" "0 255" "0 255"] -1
		]

		//attrs: <radius/emitter> <height/0> <hexcolor> <persistence>
		looplist g [0 4 7 9 10 11 12 13] [
			if (= $_egentattr_0 $g) [
				_genentcolorslider 3

				if (|| (= $_egentattr_0 0) (= $_egentattr_0 11) (= $_egentattr_0 12)) [
					guitext "radius"
					guislider _egentattr_1 0 512 [_entselupdate] 
				] [
					guitext "emitter"
					if (!= $_egentattr_0 13)[
						guilistslider _egentattr_1 $_entpemmit1 [_entselupdate]
					] [
						guilistslider _egentattr_1 $_entpemmit2 [_entselupdate]
					]
				]

				if (&& (!= $_egentattr_0 9) (!= $_egentattr_0 10)) [
					guitext "height"
					guislider _egentattr_2 0 512 [_entselupdate]
				]

				if (&& (!= $_egentattr_0 0) (!= $_egentattr_0 11) (!= $_egentattr_0 12)) [
					guitext "persistence"
					guislider _egentattr_4 0 1024 [_entselupdate]
				]
			]
		]
		guialign -1 [_genenteditbutton; guibar; _genentresetbutton 5 []]
		guialign -1 [_genentselsnapbutton; guibar; _genentselbutton]
	]

	//Platform, Elevator
	_entinsel "platform" [_genentspmsg; _genentsliders ["direction" "model" "tag" "speed"] ["0 360" [0 @@nummapmodels] "0 20" "0 500"]] "elevator"

	//Playerstart
	_entinsel "playerstart" [
		guialign 1 [_genentteams 0]
		_genentsliders ["direction"] ["0 360"]
	]
	
	//Sound
	_entinsel "sound" [_genentsliders ["type" "radius" "size"]["0 20" "0 500" "0 500"]]

	//Spotlight
	_entinsel "spotlight" [
		guialign 1 [guibutton "calclight" [calclight]; guibar; guibutton "clearlight" [clearlightmaps]]
		_genentsliders ["radius"] ["0 90"]
	]

	//Teledest
	_entinsel "teledest" [		
		guialign 1 [
			guibutton "find teleport" [entcancel; entfind teleport $_egentattr_1; if (enthavesel)[entautoview][entfind teledest * $_egentattr_1]]
			guibar; guibutton "newent teleport" [entcancel; newent teleport $_egentattr_1; entautoview]
		]
		_genentsliders ["direction" "teleport"] ["0 360" "0 20"] [guibitfield "keep momentum" _egentattr_2 1 [_entselupdate]]
	]

	//Teleport
	_entinsel "teleport" [
		guialign 1 [
			guibutton "find teledest" [entcancel; entfind teledest * $_egentattr_0; if (enthavesel)[entautoview][entfind teleport $_egentattr_0]]
			guibar; guibutton "newent teledest" [entcancel; newent teledest $_egentattr_0; entautoview]
		]
		_genentsliders ["teledest" "model" "tag"] ["0 20" [-1 @@nummapmodels] "0 20"] [guibitfield "soundless^t" _egentattr_3 "-1 0" [_entselupdate]]
	]

	if (enthavesel)[
		if (=s $_entguiname "materials")[_entguiname = (enttype); genentgui (enttype) 0]
		guitab "^f8replace"
	    guilistsplit n 3 $enttypelist [
	    	if (&& $editing (=s (at $_entcur 0) $n)) [guibutton (concatword "^f0" $n) [entautoview]] [guibutton $n [if (! $enthavesel)[newent @@n; cleargui]; entset @n; genentgui @n 0]]
	    ]
	   	guibutton "dropent" [showgui dropent]
	    guilist [guistrut 5 1]
	][
		genentgui @n 0
		looplist mat ["air" "water" "lava" "clip" "glass" "noclip" "gameclip" "death" "alpha"] [guibutton $mat [editmat @mat]]
		guibitfield "show material volumes" showmat 1
	]
	guitab "^f8misc"
    @quickeditmenu
]

genentgui = [
	_entcur = (entget);
	if (enthavesel)[if (> $enthavesel 1)[_entguiname = (concatword (at $arg1 0) "^f0(" $enthavesel ")")][_entguiname = (at $arg1 0)] ][_entguiname = "materials"]
	newgui entgui [
		guistayopen [_entcur = (entget); _genent]
	] (concatword "^f8" $_entguiname)
	if (=s $arg2 "")[showgui entgui]
]
// advanced ent gui -- end


// prefabs gui -- start
_prefabslist = (getfileslist "packages/prefab" "obr")
prefabs = [
	newgui _p [
		guilist [
			guialign -1 [
				guilistsplit p 2 $_prefabslist [
					guibutton $p [
						if (= $havesel 0) [setselpos (at (getcampos) 0) (at (getcampos) 1) (at (getcampos) 2); gotosel]
						pasteprefab @p
					] "inexor/action"
				]
			]
			guibar
			guiprefabpreview $guirollovername 0xFFFFFF [pasteprefab @guirollovername] 8 0
		]
	] "^f8Prefabs"
	showgui _p
]
// prefabs gui -- end

// hud options -- start
hudoptions = [
	guistayopen [
		guilist [
			guilist [
				guicheckbox (concatword "^f" (at ["4" "0"] $gameclock) "show remaining time") gameclock
				guilist [
					guitext "align "
					guiradio "left " gameclockalign -1
					guiradio "center " gameclockalign 0
					guiradio "right " gameclockalign 1
				]
				guilist [
					guilist [
						guitext "alpha "
						guitext "scale "
						guitext "pos. X "
						guitext "pos. Y "
					]		
					guilist [
						guilist [guinumfield gameclockalpha 4 10 []]
						guilist [guinumfield gameclockscale 4 0.02 [] "+fround" 5]
						guilist [guinumfield gameclockx 4 0.02 [] "+fround" 5]
						guilist [guinumfield gameclocky 4 0.02 [] "+fround" 5]
					]
				]
			]
			guibar
			guilist [
				guicheckbox (concatword "^f" (at ["4" "0"] $hudscore) "show score") hudscore
				guilist [
					guitext "align "
					guiradio "left " hudscorealign -1
					guiradio "center " hudscorealign 0
					guiradio "right " hudscorealign 1
				]
				guilist [
					guilist [
						guitext "alpha "
						guitext "spacing "
						guitext "scale "
						guitext "pos. X "
						guitext "pos. Y "
					]		
					guilist [
						guilist [guinumfield hudscorealpha 4 10 []]
						guilist [guinumfield hudscoresep 4 10 []]
						guilist [guinumfield hudscorescale 4 0.02 [] "+fround" 5]
						guilist [guinumfield hudscorex 4 0.02 [] "+fround" 5]
						guilist [guinumfield hudscorey 4 0.02 [] "+fround" 5]
					]
				]
			]
		]
		guibar
		guilist [
			guilist [
				guicheckbox (concatword "^f" (at ["4" "0"] $ammobar) "show ammo bar") ammobar
				guilist [
					guitext "align "
					guiradio "left " ammobaralign -1
					guiradio "center " ammobaralign 0
					guiradio "right " ammobaralign 1
					guicheckbox (at ["vertical" "horizontal"] $ammobarhorizontal) ammobarhorizontal
				]
				guilist [
					guicheckbox "hide empty " ammobarhideempty
					guicheckbox "flip settings" ammobarflip
				]
				guilist [
					guiitemmove -1 1 0 [
						guilist [
							guitext "spacing "
							guitext "scale "
							guitext "pos. X "
							guitext "pos. Y "
						]
					]
					guilist [
						guialign 0 [guitext "icons" 0]
						guilist [guinumfield ammobarsep 4 10 []]
						guilist [guinumfield ammobarscale 4 0.02 [] "+fround" 5]
						guilist [guinumfield ammobarx 4 0.02 [] "+fround" 5]
						guilist [guinumfield ammobary 4 0.02 [] "+fround" 5]
					]
					guilist [
						guialign 0 [guitext "counter" 0]
						guilist [guinumfield ammobarcountsep 4 10 []]
						guilist [guinumfield ammobarcountscale 4 0.02 [] "+fround" 5]
					]
				]
			]
			guibar
			guilist [
				guilist [
					guicheckbox (strstatecolor $hidehud "hide HUD") hidehud
					guicheckbox (strstatecolor (!=s (getalias gamehud) "") "show gamehud") cgamehud 1 0 [gamehud = (at ["" "[defaultgamehud]"] $cgamehud)]			
				]
				guicheckbox (strstatecolor $showfps "show FPS") showfps
				guicheckbox (strstatecolor $hudgun "show hudguns") hudgun
				guiitemmove -1 2 0 [
		            if $hudgun [
		                guicheckbox "sway" hudgunsway
		                guicheckbox "muzzle flash^t" muzzleflash
		                guicheckbox "muzzle light" muzzlelight
		            ]
		        ]
	            guicheckbox (concatword "^f" (at ["4" "0"] $wallclock) "show wall clock") wallclock
	            guiitemmove -1 2 0 [
		            if $wallclock [
		            	guilist [
		                	guicheckbox "24 hour^t" wallclock24
		                	guicheckbox "seconds" wallclocksecs
		                ]
		            ]
		        ]
			]

		]
	] 
]

// hud options -- end

// atmo options -- start
_skyboxeslist = (getfileslist "packages/skyboxes" "png")
_curlighttype = 1
atmooptions = [
	guistayopen [
		if (= $arg1 0) [
			guitext "^f8atmosphere"
			guicheckbox (strstatecolor $atmo "show atmosphere") atmo
			guilistsplit atmf 2 [atmoalpha atmobright atmodensity atmohaze atmoheight atmoozone atmoplanetsize atmosundiskbright atmosundisksize atmosunlightscale] [
				guialign 0 [guitext (strreplace $atmf "atmo" "") 0]
				guilist [guinumfield $atmf 5 (at [0.05 0.5] (>= $$atmf 10)) [] "+fround" 5]
			]
			guicolorslider atmosunlight 0 255 []
		]
		if (= $arg1 1) [
			guitext "^f8clouds"
			_ct = 0
			guilist [
				guilistsplit curcloud 1 $_skyboxeslist [
					guiimage (concatword "<thumbnail:32>packages/skyboxes/" $curcloud ".png") [cloudlayer (concatword "skyboxes/" @curcloud)] 1 0 "data/cube.png"
				]
			]
			guilistsplit cldf 2 [cloudalpha cloudclip cloudfade cloudheight cloudoffsetx cloudoffsety cloudscale cloudscrollx cloudscrolly] [
				guialign 0 [guitext (strreplace $cldf "cloud" "") 0]
				guilist [guinumfield $cldf 5 (at [0.05 0.5] (>= $$cldf 10)) [] "+fround" 5]
			]
			guicolorslider cloudcolour 0 255 []
		]
		if (= $arg1 2) [
			guitext "^f8fog"
			guilist [
				guicheckbox (strstatecolor $fogdomecap "domecap") fogdomecap
				guicheckbox (strstatecolor $fogdomeclouds "domeclouds") fogdomeclouds
			]

			guilistsplit fgdf 3 [fogdomeclip fogdomeheight fogdomemax fogdomemin] [
				guialign 0 [guitext (strreplace $fgdf "fog" "") 0]
				guilist [guinumfield $fgdf 5 (at [0.05 0.5] (>= $$fgdf 10)) [fog $foglevel] "+fround" 5]
			]
			guialign 0 [
				guilist [
					guialign 0 [guitext "fog distance" 0]
					guilist [guinumfield fog 8 16 []]
				]
			]
			guicolorslider fogcolour 0 255 []
		]
		if (= $arg1 3) [
			guilist [
				guitext "^f8lighting"
				guicheckbox (strstatecolor $fullbright "fullbright") fullbright
				guistrut 4.15
					
			]
			guilist [
				guibutton "calclight^t" [calclight]
				guibutton "patch" [patchlight]
			]
			guilist [
				guibutton "remip^t" [remip]
				guibutton "recalc" [recalc]
			]
			guialign 0 [
				guilist [
					guialign 0 [guitext (strstatecolor (= (indexof [512 32 16 1] $lightprecision) -1) "lightprecision" ~ 2) 0]
					guilist [
						guiradio (strstatecolor (= $lightprecision 512) "fast" 4 0) lightprecision 512
						guiradio (strstatecolor (= $lightprecision 32) "default" 4 1) lightprecision 32
						guiradio (strstatecolor (= $lightprecision 16) "pretty" 4 6) lightprecision 16
					]
					guialign 0 [guilist [guilist [guinumfield lightprecision 5 15 []]]]
				]
			]
			guialign 0 [
				looplist lgdf [lighterror lightthreads] [
					guilist [
						guialign 0 [guitext $lgdf 0]
						guialign 0 [guilist [guilist [guinumfield $lgdf 3 1 []]]]
					]
				]
			]
			guialign 0 [guitext "light type" 0]
			guialign 0 [
				guiradio (strstatecolor (= $_curlighttype 0) "skylight") _curlighttype 0
				guiradio (strstatecolor (= $_curlighttype 1) "sunlight") _curlighttype 1
			]
			guicolorslider (at [skylight sunlight] $_curlighttype) 0 255 []
			if (= $_curlighttype 1) [
				guibutton "getsundir" [getsundir]
			]
		]
	]
]

// atmo options -- end
