// handles gamemodes, maps and map poll

// note: a single map can also belong to two or more lists

// has pickups
ffamaps1 = ["blank" "frag-lab" "gunmetal" "valkyria" "collide" "luna" "mc-lab" "pandora" "breakout" "hurricane3" "laucin" "absolute" "outpost" "tubes"]

// has flags
ctfmaps1 = ["collide" "luna" "mc-lab" "pandora" "breakout" "hurricane3" "deadworld" "laucin" "tubes"]

// has bases
capturemaps1 = ["collide" "luna" "mc-lab" "pandora" "deadworld" "outpost"]

allmaps = (concat $ffamaps1 $capturemaps1 $ctfmaps1)
allmaps = (listunrepeat $allmaps)

//gamemode "attributes" icons
_geticonsfromlist = [
  _ilist = (loopconcat n 23 [$n])
  _it = (at [[[-][5 6 7 15 16 17 18 19 20 21 22]][[+][23]][[-][5 6 7 15 17 18 19 20 21 22]][[+][12]][[c][3][16]][[+][1 8 9 10 11 12 13]][[c][5][16]][[+][19 20]][[c][7][16]][[c][8][17]][[c][8][18]][[c][0][16 21]][[c][3][16 21]][[c][0][16 22]][[c][3][16 22]][[c][0][16 5]][[c][3][16 5]][[c][5][16 21]][[c][5][16 22]][[c][5][16 5]][[c][0][16 15]][[c][3][16 15]][[c][5][16 15]]] $arg1)
  if (=s (at $_it 0) "-")[ //blacklist
    looplist il (at $_it 1) [
      if (> (indexof $_ilist $il) -1)[_ilist = (listreplace $_ilist (indexof $_ilist $il) "")]
    ]
  ]
  if (=s (at $_it 0) "+")[ //whitelist
    _ilist = []
    looplist il (at $_it 1) [append _ilist $il]
  ]
  if (=s (at $_it 0) "c")[ //copy and add
    _ilist = []
    _geticonsfromlist (at $_it 1) (at $_it 2)
  ]
  if (!=s $arg2 "") [looplist i $arg2 [append _ilist $i]]
  result $_ilist
]

_getmodeicons = [
  guilist [
    looplist i (_geticonsfromlist $arg1) [
      guiimage (concatword "packages/icons/modes/" (at ["bluearmour" "greenamour" "yellowarmour" "health" "quad" "flag" "flag_b" "flag_r" "fist" "shotg" "chaing" "rocket" "rifle" "gl" "pistol" "skull" "team" "capture" "regencapture" "rndgun" "rndarmour" "ctf" "protect" "editing"] $i) ".png") [echo (concatword "^f6" (at $modenames @arg1) "^f~: " (strreplace (at ["^f~Instant ^f1blue armour ^f~when spawning (^f0+50 ^f7health^f~)" "^f0green armour ^f~pickups are available (^f0+100 ^f7health^f~)" "^f2yellow armour ^f~pickups are available (^f0+200 ^f7health^f~)" "^f8health ^f~and ^f8healthboost ^f~pickups are available (^f0+25 ^f7health / ^f0+50 ^f7health limit^f~)" "^f8quaddamage ^f~pickups are available (^f04x ^f7more damage^f~)" "^f4gray flag ^f~must be held for 20 seconds" "^f1blue flag ^f~must be protected" "^f3red flag ^f~must be stolen" "^f8chainsaw ^f~weapon is available (^f8default key: 0^f~)" "^f8shotgun ^f~weapon is available (^f8default key: 1^f~)" "^f8chaingun ^f~weapon is available (^f8default key: 2^f~)" "^f8rocket ^f~weapon is available (^f8default key: 3^f~)" "^f8rifle ^f~weapon is available (^f8default key: 4^f~)" "^f8grenade launcher ^f~weapon is available (^f8default key: 5^f~)" "^f8pistol ^f~weapon is available (^f8default key: 6^f~)" "^f8skulls ^f~must be collected" "^f1good ^f~and ^f3evil ^f~teams are available" "^f8bases ^f~must be captured (^f0extra ammo spawns in allied bases^f~)" "^f8bases ^f~must be captured (^f0regenerate health and ammo next to allied bases^f~)" "^f5random weapons ^f~when spawning" "^f5random armour ^f~when spawning" "^f3red flag ^f~must be stolen and brought to the ^f1blue flag" "^f3red flag ^f~must be touched, ^f1blue flag ^f~can be held" "^f~all ^f8pickups ^f~are available, ^f8geometry editing ^f~is allowed and players can fly (^f8default key: E^f~)"] @i) "^^f" []))] 0.5 0
    ]
  ]
]

genmapitems = [
  local curmap
  guilistsplit curmap 2 $arg1 [
    guibutton $curmap [mode $_gmode; map @curmap] (if (=s $guirollovername $curmap) [if (|| (= (at (numplayers) 0) 0) (&& (!= (getmastermode) 0) (haspriv (getclientnum)))) [result "arrow_fw.jpg"] [result "icons8/thumbsup.png"]] [result "inexor/action.jpg"])
  ]
]

showmapshot = [
    guibar
    guiimage (concatword "packages/base/" (if (> $numargs 0) [result $arg1] [at $guirollovername 0]) ".jpg") $guirolloveraction 4 1 "data/cube.png"
]

gettopvotes = [
  local curbest topvotes
  curbest = (at $listvotes 0)
  loop i (listlen $listvotes) [
    if (< (at $curbest 2) (at (at $listvotes $i) 2)) [
      curbest = (at $listvotes $i)
    ]
  ]
  loop i (listlen $listvotes) [
    if (= (at (at $listvotes $i) 2) (at $curbest 2)) [
      append topvotes (concatword "[" (at $listvotes $i) "]")
    ]
  ]
  result $topvotes
]

getvotesnum = [local n; n = 0; looplist v $listvotes [n = (+ $n (at $v 2))]; result $n]

guimappoll = [
  guitab (format "^f7%1 ^f8%2 in poll" (listlen $listvotes) (at ["map" "maps"] (> (listlen $listvotes) 1)))
  local mpcurvote mpcurmap mpistopvoted mpimagesize
  mpimagesize = (maxf 2.4 (- (at [8.5 7.5] (= (listlen $listvotes) 1)) (listlen $listvotes)))
  if (listlen (getalias listvotes)) [
    guialign 0 [
      guilist [
        loop row (div (+ (listlen $listvotes) 5) 6) [
          guilist [
            loop col 6 [
              mpcurvote = (at $listvotes (+ (* $row 6) $col))
              mpcurmap = (at $mpcurvote 0)
              if (> (at $mpcurvote 2) 0) [
                if (=s $mpcurmap "") [mpcurmap = "[new map]"]
                if (>= (strlen $mpcurmap) 9) [mpcurmap = (strscroll $mpcurmap 9 150 0)]
                mpistopvoted = (!= (indexof (gettopvotes) $mpcurvote) -1)
                _mpisownvote = (!= (indexof (at $mpcurvote 3) (getclientnum)) -1)
                if (strlen $mpcurvote) [
                  guilist [
                    guiimage (format "<mad:%1>packages/skyboxes/plain/white_ft.png" (
                      ? $_mpisownvote "0/0.7/0" (? $mpistopvoted "0/1/1" "0.2/0.2/0.2")
                    )) [] (+f 0.1 $mpimagesize) 0 "data/background.png"

                    // map thumb
                    guiitemmove 0 0 (concatword "-" (maxf 4.9 (+f (*f $mpimagesize 2) 0.1))) [
                      guiimage (concatword "packages/base/" $mpcurmap ".jpg") [
                        mpcurvote = (at $listvotes (+ (* @row 6) @col))
                        mode (at $mpcurvote 1); map (at $mpcurvote 0)
                      ] $mpimagesize 0 "packages/textures/default.png"

                      // stripe
                      guiitemmove 0 0 (concatword "-" (maxf -6 (*f $mpimagesize 2))) [
                        guiimage "data/stripe.png" [] $mpimagesize
                        guiitemmove -1 0.5 (concatword "-" (maxf -6 (*f $mpimagesize 2))) [
                          guititle (concatword "^f6" (at $modenames (at $mpcurvote 1)))
                        ]
                      ]

                      // counter
                      guiitemmove 0 0 (concatword "-" (+f $mpimagesize 0.8)) [
                        guititle (concatword (strstatecolor $mpistopvoted (at $mpcurvote 2) "3" "8") " " (at ["vote" "votes"] (> (at $mpcurvote 2) 1)))
                        // percentage
                        guialign 0 [guitext (strstatecolor $mpistopvoted (concatword (div (* 100 (at $mpcurvote 2)) (getvotesnum)) "^%") "3" "8") 0]
                      ]

                      // map name
                      guiitemmove 0 0 (concatword "-" (maxf 1.2 (*f 0.6 (divf $mpimagesize 2)))) [
                        guitext (concatword "^f7" $mpcurmap) 0
                      ]

                      // check box
                      guiitemmove -1 -0.2 (concatword "-" (maxf 1 (*f 0.6 (divf $mpimagesize 2)))) [
                        if $_mpisownvote [
                          guiimage "<mad:0/1/0>packages/icons/checkbox_on.jpg" [] 0.5 0 "packages/icons/arrow_fw.jpg"
                        ] [guiimage "packages/icons/checkbox_off.jpg" [] 0.5 0]
                      ]
                    ]
                  ]
                ]
              ]
            ]
          ]
        ]
      ]
    ]
  ] [guitext "^f3There is no map poll in progress"]
]

_gmode = $mode
_listmodemaps = 0
_listmodemodifiers = $_gmode
_amm = ["all" "items" "flags" "bases"]
newgui gamemode [
  guistayopen [
    guilist [
      guilist [
        guiitemmove 0 0 0 [
          guialign 0 [guitext "^f8mode attributes" 0]
          _getmodeicons $_gmode
        ]
        guiitemmove 1 0 -2 [guibutton "^f4info" [echo (concatword "^f7" (getmodeinfo $_gmode))] 0]
        guiitemmove -1 0 0 [
          guilistsplit cmode 2 ["FFA" "coop-edit" "teamplay" "instagib" "efficiency" "tactics" "capture" "CTF" "protect" "hold" "collect"] [
            guistrut 13 1
            guiradio (concatword "^f0" $cmode) _listmodemodifiers (indexof $modenamesex $cmode) [_gmode = $_listmodemodifiers]
            if (= $_listmodemodifiers (indexof $modenamesex $cmode)) [
              guiitemmove -1 2 0 [
                looplist cmodf (listreplace (listmatch $modenamesex $cmode) 0 "") [
                  guiradio (strreplace (strreplace $cmodf $cmode "") " " "") _gmode (indexof $modenamesex $cmodf)
                ]
                if (!= $_listmodemodifiers $_gmode)[guibutton "modifiers" [_gmode = $_listmodemodifiers] "inexor/exit"]
              ]
            ]
          ]
        ]
        guistrut 28 1
      ]
      //guitab (concatword "^f8maps (" (strlower (at $_amm $_listmodemaps)) ")")
      guibar
      guilist [
        guialign 1 [
          if (integration) [
            guibutton "^f5workshop" [_updateworkshop; showgui workshopgui] "icons8/steam.png"
            guistrut 1
          ]
          if (|| $editing (m_edit (getmode))) [guialign 1 [guibutton "custom maps" [showcustommaps] "icons8/folder.png"]]
        ]
        guialign 0 [guitext "^f8available entities" 0]
        guialign 0 [
          _cmodfu = (strupper (at $modenames $_gmode))
          if (|| (> (strstr $_cmodfu "CTF") -1) (> (strstr $_cmodfu "PROTECT") -1)) [_listmodemaps = 2]
          if (|| (> (strstr $_cmodfu "HOLD") -1) (> (strstr $_cmodfu "CAPTURE") -1)) [_listmodemaps = 3]
          looplist _mm $_amm [
            guiradio $_mm _listmodemaps (indexof $_amm $_mm) [
              if (= (strstr [1 0 11 9] (at [1 0 11 9] (indexof $_amm @_mm))) -1) [
                _listmodemodifiers = (at [1 0 11 9] (indexof $_amm @_mm))
                _gmode = $_listmodemodifiers
                _mapsfromlist = [result (at [allmaps ffamaps1 ctfmaps1 capturemaps1] (indexof $_amm @@_mm))]
                guirollovername = (at $(_mapsfromlist) (rnd (listlen $(_mapsfromlist))))
                guirolloveraction = [mode $_gmode; map $guirollovername]
              ]
            ]
          ]
        ]
        guibar
        guilist [
          guistrut (div (listlen $allmaps) 2) 1
          guilist [
            guistrut 20 1
            genmapitems $(at [allmaps ffamaps1 ctfmaps1 capturemaps1] $_listmodemaps)
          ]
          showmapshot
          guiitemmove -1 4.7 -15.7 [
            guititle (concatword (loopstr "^t" 3) "^n" (if (> (indexof $allmaps (at $guirollovername 0)) -1) [result (concatword "^f6" (at $modenames $_gmode) "^n^f7" (at $guirollovername 0))] [result ""; guirolloveraction = [echo "^f3select a map first"]]))
          ]
        ]
        guistrut 3 0
      ]
    ]
    if (!=s (getalias listvotes) "") [
      guimappoll
    ]
  ]
] "^f8mode & map"

//custom maps
_ctxmap = ""
_mmsearch = ""
newgui mymaps [
  guistayopen [
    guilist [
      guilist [
        guilistscroll curmap $mymaps [
          if (> (strlen $curmap) 15) [_curmapm = (strscroll $curmap 15 150 0)] [_curmapm = $curmap] //slides text
          guilist [
            if (&& (!=s $_mmsearch "") (!= (strstr $curmap $_mmsearch) -1)) [guitext "^f7>" 0]
            guibutton (strstatecolor (=s $_ctxmap $curmap) $_curmapm "1" "0") [
              if (=s $_ctxmap @curmap) [map @@curmap]
              _ctxmap = @curmap
            ] "icons8/folder.png"
          ]
        ] 10 1 [
          guilist [
            guitext (concatword "^f4(" (listlen $mymaps) ") ") 0
            guifield _mmsearch 10 [
              _curfoundmap = (indexof $mymaps (at (substr $mymaps (strstr $mymaps $_mmsearch)) 0))
              if (& (!= $_curfoundmap "") (!= $_curfoundmap -1)) [
                _curtabcurmap = (max 0 (min (- (listlen $mymaps) 10) $_curfoundmap))
                _ctxmap = (at $mymaps $_curfoundmap)
              ]
            ]
          ]
        ] [
          guitextbox "There are no maps yet.^nwhile editing, a custom map can be saved using the ^f8/savemap mapname ^f~command. Maps are ^f8.ogz ^f~files saved in the ^f8Tomatenquark/packages/base ^f~folder by default.^nThis menu can be shown through the interface in ^f8ESC > editing^f7, or by typing the command ^f8/showmymaps^f7." 35 7
          guibar
          guibutton "^f2advanced editing commands" [showgui editing] "arrow_fw"
        ]
        guistrut 20 1
      ]
      guibar
      if (=s $_ctxmap "") [_ctxmap = (at $mymaps 0)]
      guilist [loadcustommap $_ctxmap]
    ]
    //guitab "^f1Steam Workshop"
    //workshopgui
  ]
] "^f8Local Maps"

showmymaps = [
    mymaps = ""
    loopfiles curmap "packages/base" "ogz" [
        if (< (indexof $allmaps $curmap) 0) [
            append mymaps $curmap
        ]
    ]
    showgui mymaps
]

showcustommaps = [showmymaps]

loadcustommap = [
  local _ctxmap _mapname
  _ctxmap = $arg1; _mapname = $_ctxmap

  guibutton "load and edit" [coop $_ctxmap; edittoggle; if (integration) [_updateworkshop]] "modes/editing.png"
  guibutton "^f5Share to Gallery" [
    if (integration) [
      if (isconnected) [_updateworkshop; showgui workshopgui] [echo "^f5[Workshop] ^f3Load a map first"]
    ] [echo "^f5[Workshop] ^f1Workshop ^f2features are only available on the ^f1Steam ^f2version of ^f6Tomatenquark.^n^f5steampowered.com/app/1274540^n^f7tomatenquark.org"]
  ] "icons8/steam.png"
  guitext "^f7map name"
  guialign 0 [guifield _mapname -15 [if (!=s $_ctxmap $_mapname) [if (&& (findfile (concatword "packages/base/" $_mapname ".ogz"))) [echo "^f7there is already a map with this name"; _mapname = $_ctxmap] [renamemap $_ctxmap $_mapname; _ctxmap = $_mapname; showcustommaps]]]]
  guialign 0 [guiimage (concatword "<thumbnail: 256>packages/base/" $_ctxmap "." (at ["png" "jpg"] (findfile (concatword "packages/base/" $_ctxmap ".jpg")))) [map $_ctxmap; if (integration) [_updateworkshop]] 4 0 "packages/textures/nomapthumb.png"]
  guialign 0 [
    guibutton "config.." [notepad (concatword "packages/base/" $_ctxmap ".cfg")]
    guibar
    guibutton "delete" [
      newgui _tmp2 [
        guitext (concatword "are you sure you want to delete ^f8" $_ctxmap "^f~.ogz?")
        guitext "existing backup (.bak) files will be kept intact."
        guibar
        guialign 0 [
          guibutton "return" [cleargui 1] "arrow_bw"
          guibar
          guibutton "confirm" [delmap $_ctxmap; showcustommaps] "trash.png"
        ]
      ] 0
      showgui _tmp2
    ] "trash.png"
  ]
]