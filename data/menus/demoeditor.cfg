demoeditor = [showgui demomanage]

// remove special characters
_dmm_fixstr = [$arg1 = (strreplace+ $arg1 ["^"" " " "^n" "^t"] ["" "-" "" ""]]

initvar _dmm_valids (loopconcatword n 60 [result $n])
initvar _dmm_minutes 9
initvar _dmm_seconds 35
initvar _dmm_demoname ""

initvar mymovie_dmm_actionslistCMD ""
initvar mymovie_dmm_actionslistTIME ""

_dmm_goto = [
	gamespeed 1000
	if (!= $arg1 "")[_dmm_goingto = $arg1]
	if (> $_dmm_goingto $timeremaining) [
		if (=s $_dmm_demoname "")[
			echo "^f7You can't travel backwards without setting the demo name."
			clearsleep
			gamespeed 100
			pausegame 1
		] [
			demo $_dmm_demoname
			clearsleep
			sleep 1 [_dmm_goto]
		]
	] [
		if (!= $timeremaining $_dmm_goingto) [
			sleep 1 [_dmm_goto]
		] [
			pausegame 1
			echo "^f7Done, game paused."
			gamespeed 100
		]
	]
]

initvar _dmm_showlist 1
_dmm_gettimeline = [
	local _dmm_ct _dmm_ct2 curtime
	_dmm_ct = 0; _dmm_ct2 = 0

	guitab "^f7timeline"
	guititle "";
	guistrut -1
	guitext "^f4[-----------------------------------]" 0

	looplist curcmd $(concatword $_dmm_projectname "_dmm_actionslistCMD") [
		curtime = (at $(concatword $_dmm_projectname "_dmm_actionslistTIME") $_dmm_ct)
		if (> $curtime $timeremaining)[_dmm_statecolor = "^f0"]
		if (< $curtime $timeremaining)[_dmm_statecolor = "^f4"]
		if (= $curtime $timeremaining)[_dmm_statecolor = "^f6"]
		guiitemmove -1 (divf $curtime 30) -1 [
			guibutton (concatword  $_dmm_statecolor "|") [echo (format "^f7[^f0%1^f7]: ^f2%2:%3" @curcmd (div $curtime 60) (mod $curtime 60))] 0
		]
		_dmm_ct = (+ $_dmm_ct 1)
	]
	guiitemmove -1 (-f (divf $timeremaining 30) 0.95) -1 [guitext "^f5<]" 0]
	guiitemmove -1 (-f (divf $timeremaining 30) 1.5) -1.7 [guititle (concatword "^f5" (timeremaining 1))]
	
	guialign 0 [
		if (!= $_dmm_actionrun 1)[
			if (> (strlen (getalias (concatword $_dmm_projectname "_dmm_actionslistCMD"))) 0)[
				guibutton "Run actions" [clearsleep; _dmm_actionexec; pausegame 0] "checkbox_off"
			] [
				guibutton "^f4Run actions" [echo "^f7Add some actions."]
			]
		] [
			guibutton "^f2Stop actions" [_dmm_actionrun = 0; clearsleep; pausegame 1] checkbox_on;
		]
	]
	if (&& (= $paused 0) (= $_dmm_actionrun 0))[
		guitextbox "the game is running but actions are disabled..." 20
	]
	if (&& (= $paused 1) (= $_dmm_actionrun 1))[
		guitextbox "the actions are enabled but the game is paused..." 20
	]
	guibar
	guialign 1 [guibitfield "^f7Show list" _dmm_showlist 1]
	if (_dmm_showlist)[
		looplist curcmd $(concatword $_dmm_projectname "_dmm_actionslistCMD") [
			curtime = (at $(concatword $_dmm_projectname "_dmm_actionslistTIME") $_dmm_ct2)
			if (> $curtime $timeremaining)[_dmm_statecolor = "^f0"]
			if (< $curtime $timeremaining)[_dmm_statecolor = "^f4"]
			if (= $curtime $timeremaining)[_dmm_statecolor = "^f6"]
			guialign 1 [
				guitext (format "^f7[%1%2^f7] ^f1%3:%4" $_dmm_statecolor $curcmd (div $curtime 60) (mod $curtime 60)) 0
				guibar
				guibutton "" [
					(concatword $_dmm_projectname "_dmm_actionslistTIME") = (listsplice $(concatword $_dmm_projectname "_dmm_actionslistTIME") "" @_dmm_ct2 1)
					(concatword $_dmm_projectname "_dmm_actionslistCMD") = (listsplice $(concatword $_dmm_projectname "_dmm_actionslistCMD") "" @_dmm_ct2 1)
				] "inexor/exit"
			]
			_dmm_ct2 = (+ $_dmm_ct2 1)
		]
	]
]

_dmm_actionexec = [
	if (=s $_dmm_projectname "")[echo "^f7Insert a project name first."] [
		_dmm_actionrun = 1
		loop cmd (listlen $(concatword $_dmm_projectname "_dmm_actionslistCMD")) [	
			if (= $cmd (- (listlen $(concatword $_dmm_projectname "_dmm_actionslistCMD")) 1))[
				sleep 1000 [_dmm_actionexec]
			]
			if (= $timeremaining (at $(concatword $_dmm_projectname "_dmm_actionslistTIME") $cmd))[
				//dmm_lastusedframe = $timeremaining
				do (at $(concatword $_dmm_projectname "_dmm_actionslistCMD") $cmd)
			]
		]
	]
]
_dmm_actionrun = 0

if (=s (getalias _demomanage_actions) "") [_demomanage_actions = ""];
if (=s (getalias _dmm_projectname) "")[
	_dmm_projectname = "mymovie";
]
if (=s (getalias (concatword $_dmm_projectname "_dmm_actionslistCMD")) "")[
	(concatword $_dmm_projectname "_dmm_actionslistCMD") = ""
]

dmm_addactionlist = [
	if (=s $_dmm_projectname "")[echo "^f7Insert a project name first."] [
		(concatword $_dmm_projectname "_dmm_actionslistCMD") = (concatword $(concatword $_dmm_projectname "_dmm_actionslistCMD") " ^"" $arg1 "^"")
		(concatword $_dmm_projectname "_dmm_actionslistTIME") = (concatword $(concatword $_dmm_projectname "_dmm_actionslistTIME") " ^"" $arg2 "^"")

		echo (concatword "^f7new action set to ^f0" $_dmm_projectname " ^f7in ^f0" $_dmm_minutes ":" $_dmm_seconds "^n^f7>" $(concatword $_dmm_projectname "_dmm_actionslistCMD"))
		//echo $(concatword $_dmm_projectname "_dmm_actionslistTIME")
	]
]

newgui demomanage [
	guilist [guistrut 25]
	guistayopen [
		if (|| (= $timeremaining 0) (= $isconnected 0))[
			clearsleep;
			guitext "timeline unavailable^nin this mode"
		] [
			guititle (concatword "^f0" (timeremaining 1))
		]
		guititle ""
		guialign 0 [
			guifield _dmm_minutes -5 [
				if (> $_dmm_minutes 9)[_dmm_minutes = 9]
				if (|| (< $_dmm_minutes 0) (=s $_dmm_minutes ""))[_dmm_minutes = 0]
				if (= (strstr 0123456789 $_dmm_minutes) -1)[echo "^f7Only numbers from 0 to 9 are allowed here."; _dmm_minutes = 0]
			]
			guibar
			guifield _dmm_seconds -5 [
				if (> $_dmm_seconds 59)[_dmm_seconds = 0 ; if (< $_dmm_minutes 9)[_dmm_minutes = (+ $_dmm_minutes 1)][_dmm_minutes = 9 ; _dmm_seconds = 59]]
				if (|| (< $_dmm_seconds 0) (=s $_dmm_seconds ""))[_dmm_seconds = 0]
				if (= (strstr $_dmm_valids $_dmm_seconds) -1)[echo "^f7Only numbers from 0 to 60 are allowed here."; _dmm_seconds = 0]
			]
		]
		guialign 0 [guibutton (concatword "jump to " $_dmm_minutes ":" $_dmm_seconds) [pausegame 0; _dmm_goto (+ (* $_dmm_minutes 60) $_dmm_seconds)] 0]
		guibar
		guialign -1 [guitext "project name^f7* " 0]
		guifield _dmm_projectname -15 [
			_dmm_fixstr _dmm_projectname
			if (> (strlen $_dmm_projectname) 0)[
				if (=s (getalias (concatword $_dmm_projectname "_dmm_actionslistCMD")) "")[
					echo (concat "^f7A new project has been created:^f0" $_dmm_projectname)
					(concatword $_dmm_projectname "_dmm_actionslistCMD") = "";
					(concatword $_dmm_projectname "_dmm_actionslistTIME") = "";
				] [
					echo (concat "^f7A previous project was loaded:^f0" $_dmm_projectname)
				]
			]
		]
		guialign -1 [guitext "demo name^f7* " 0]
		guifield _dmm_demoname -15 []

		guialign -1 [
			if (> (strlen (getalias (concatword $_dmm_projectname "_dmm_actionslistCMD"))) 0)[
				guibutton "^f7clear all actions" [
					echo (concatword "^f7" (listlen $(concatword $_dmm_projectname "_dmm_actionslistCMD")) " action(s) have been cleared from the project: ^f0" $_dmm_projectname)
					(concatword $_dmm_projectname "_dmm_actionslistCMD") = []
					(concatword $_dmm_projectname "_dmm_actionslistTIME") = []
				] "inexor/exit"
			] [
				guibutton "^f4clear all actions" [echo "^f7There is no actions to clear."] "inexor/exit"
			]
			guibar
			guibutton "^f7save/Load" [showgui _dmm_share] "arrow_fw"
		]
		guialign -1 [guitext "action: " 0]
		guialign 0 [guieditor $_demomanage_actions 20 5]
		guialign 0 [
			guibutton (concatword "^f0> ^f7add action to " $_dmm_minutes ":" $_dmm_seconds)[
				textfocus $_demomanage_actions;
				if (=s $textshow "")[echo "^f7Missing action."] [
					dmm_addactionlist $textshow (+ (* $_dmm_minutes 60) $_dmm_seconds)
				]
			] 0
		]
		
		if (> (strlen $_dmm_projectname) 0)[
			if (> (listlen $(concatword $_dmm_projectname "_dmm_actionslistCMD")) 0)[
				if (|| (= $timeremaining 0) (= $isconnected 0))[][
					_dmm_gettimeline
				]
			]
		]
	]
] "^f7demo editor"

newgui _dmm_share [
	if (=s (getalias _dmm_share_name) "")[_dmm_share_name = (concatword $_dmm_projectname ".cfg")]
	guistayopen [
		guialign -1 [
			guititle "^f7File: "
			guifield _dmm_share_name -15 [
				if (> (strlen $_dmm_share_name) 0)[if (= (strstr $_dmm_share_name ".") -1)[_dmm_share_name = (concatword $_dmm_share_name ".cfg")]]
			]
		]
		guialign -1 [
			guibutton "^f0> ^f7Import actions" [
				if (= (strstr $_dmm_share_name "/") -1)[
					exec (concatword "demoeditor/" $_dmm_share_name)
				] [exec $_dmm_share_name]
			] 0
		]
		guialign -1 [
			guibutton "^f0> ^f7Export actions" [
				textfocus _dmm_share_project
				if (=s $textshow "")[
					echo "^f7Could not save your actions, try again"
				] [
					textsave (concatword "demoeditor/" $_dmm_share_name)
					echo (concatword "^f7Project saved in demoeditor/" $_dmm_share_name)
				]
			] 0
		]
		guialign -1 [
			guibutton "^f0> ^f7Send to maptitle" [
				textfocus _dmm_share_project
				maptitle $textshow
			] 0
		]
		guialign -1 [guibutton "^f7back" [showgui demomanage] arrow_bw]
		_dmm_share_cmd = $(concatword $_dmm_projectname "_dmm_actionslistCMD")
		_dmm_share_time = $(concatword $_dmm_projectname "_dmm_actionslistTIME")
		_dmm_share_project = (concatword "echo [^f7new project imported: ^f0" $_dmm_projectname " ^f7with " (listlen $_dmm_share_cmd) " actions.]^n" (concatword $_dmm_projectname "_dmm_actionslistCMD") " = [" $_dmm_share_cmd "]^n" (concatword $_dmm_projectname "_dmm_actionslistTIME") " = [" $_dmm_share_time "]")
		guiitemmove -1 -500 0 [guifield _dmm_share_project 1]
	]
] "^f7Import & Export"
