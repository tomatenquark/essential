skies1 = [
"skyboxes/cruz/amazon"
"skyboxes/cruz/antartic"
"skyboxes/cruz/ethna"
"skyboxes/cruz/myth"
"skyboxes/cruz/rockwell"
"skyboxes/cruz/rune"
"skyboxes/djtex/isle"
"skyboxes/djtex/isless"
"skyboxes/djtex/mountains"
"skyboxes/penguins/arid"
"skyboxes/penguins/barren"
"skyboxes/penguins/divine"
"skyboxes/penguins/gloom"
"skyboxes/penguins/harmony"
"skyboxes/penguins/wrath"
"skyboxes/penguins/yonder"
"skyboxes/penguins/zeus"
"skyboxes/lordkv/kvstar1K"
]

genskyitems = [guilistsplit cursky 4 $arg1 [showskyshot $cursky (concat skybox $cursky)]]

showskyshot = [
	_curskythumb = (concatword (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) "_ft.");
	guiimage (concatword "<thumbnail:32>packages/" $_curskythumb (at ["jpg" "png"] (findfile (concatword "packages/" $_curskythumb "png")) )) $arg2 1 1 "data/cube.png"
]

newmapsize = 12; savemap_name = temp; _curskyoption = 0; _curtex = 0
newgui editing [
	guilist [
		guilist [
			guitext "^f8command"
			guibutton "materials / entity" [genentgui (entget)]
			guibutton "toggle edit mode" [edittoggle]
			guitext "toggle heightmap"
			guibutton "deselect" [cancelsel]
			guibutton "copy" [copy]
			guibutton "paste" [paste]
			guibutton "flip / mirror cube" [flip]
			guibutton "undo" [undo]
			guibutton "redo" [redo]
			guibutton "delete" [editdel]
			guibutton "texture palette" [showtexgui]
			guibutton "mapmodels list" [showgui mapmodels]
			guibutton "select all in box" [entselect insel]
			guibutton "select all matching" [selentfindall]
			guibutton "edit entity console" [selentedit]
			guicheckbox "toggle outline" [outline]
			guicheckbox "toggle wireframe" [wireframe]
		]
		guilist [
			guitext "^f8shortcut"
			looplist sc ["F3" "E" "H" "SPACE" "C" "V" "X" "Z, U" "I" "BACKSPACE" "F2" "F4" "keypad ENTER" "N" "PERIOD" "7" "8"] [
				guitext (concatword "^f2(" $sc ")") 0
			]
		]
		guibar
		guilist [
			guitext "^f8mouse action"
			guitext "push/pull selection"
			guitext "push the face of a cube "
			guitext "change gridsize"
			guitext "change textures"
			guitext "rotate"
			guitext "push corner with cursor"
			guitext "push/pull heightmap"
			guitext "change heightmap brush"
			guitext "toggle heightmap tex"
			guitext "select"
			guitext "select corners"
			guitext "extend selection"
			guitext "reorient"
			guitext "move selection"
		]
		guilist [
			guitext "^f8shortcut"
			looplist sc ["scroll only" "hold F + scroll" "hold G + scroll" "hold Y + scroll" "hold R + scroll" "hold Q + scroll" "hold LALT + scroll" "hold B + scroll" "MMB in HM mode" "drag left mouse" "drag middle mouse" "right mouse" "right mouse" "drag right mouse"] [
				guitext (concatword "^f2(" $sc ")") 0
			]
		]
	]
	guitab "^f8entity"
	guilist [
		guilist [
			guibutton "mapmodels.." [showgui mapmodels]
			guicheckbox "mapmodel warnings" [mapmodelwarnings]
			guibutton "light.." [resetlight; showgui newlight]

			guibutton "newent playerstart^t"
			guibutton "newent shells"
			guibutton "newent bullets"
			guibutton "newent rockets"
			guibutton "newent riflerounds"
			guibutton "newent grenades"
		]
		guilist [
			guibutton "teleport.."	[showgui newteleport]
			guibutton "monster.."	[showgui monsters]
			guibutton "trigger.."	[showgui triggers]

			guibutton "newent cartridges"
			guibutton "newent health"
			guibutton "newent healthboost"
			guibutton "newent greenarmour"
			guibutton "newent yellowarmour"
			guibutton "newent quaddamage"
		]
		guibar
		guilist [
			@dropentmenu
			guibar
			@entfindmenu
		]
	]
	
	
	guitab "^f8texture blend"
	guistayopen [
		guilist [
			guialign 0 [
				local curtexname curvlayer; curtexname = (gettexname $_curtex); curvlayer = (getvlayer (getseltex))
				guiimage (concatword "packages/" $curtexname) [cleargui; showtexgui] 5 0
				guistrut -1
				guibutton (concatword ".." (substr $curtexname (strstrlast $curtexname "\"))) [echo @curtexname] 0
				guilist [
					guibutton "apply vlayer" [if (havesel) [vlayer $_curtex; blendpaintmode (? (= $blendpaintmode 0) 1 $blendpaintmode)] [echo "^f7select an area first"]]
					guinumfield _curtex 4 1 []
				]
				if (havesel) [
					guibutton (concat "selected tex:" (getseltex)) [_curtex = (getseltex)] "info"
					if $curvlayer [
						guibutton (concat "selected vlayer:" $curvlayer) [if (= $_curtex @curvlayer) [gotosel; blendpaintmode 1; cleargui] [_curtex = @@curvlayer]] "info"
						guitextbox (? (= $blendpaintmode 0) "^f2select a paint mode to use in the selected area" "^f1you are ready to paint in the selected area") 21 0
					] [guitextbox "^f6the selected area does not have a vlayer" 20 0]
				] [guitext "^f7select an area first" 0]
				guicheckbox (strstatecolor (!= $blendpaintmode 0) "paint mode") blendpaintmode 1
				guibutton "refresh blendmap" [calclight -1; fullbright 0]
			]
			guibar
			guilist [
				guitext "^f8command"
				guitext "paint blendmap"
				guibutton "change blend brush" [nextblendbrush]
				guibutton "rotate blend brush" [rotateblendbrush]
				guibutton "clear entire blendmap" [clearblendmap]
				guibutton "clear blendmap in selection" [clearblendmapsel]
				loop pm 5 [guiradio (strstatecolor (= $blendpaintmode $pm) (format "blendpaintmode %1" (at $blendpaintmodes $pm) $pm)) blendpaintmode $pm]

			]

			guilist [
				guitext "^f8shortcut"
				looplist sc ["left mouse" "scroll" "right mouse" "clearblendmap command" "clearblendmapsel command" "keypad 0" "keypad 1" "keypad 2" "keypad 3" "keypad 4"] [
					guitext (concatword "^f2(" $sc ")") 0
				]
			]
		]

		guitab "^f8atmosphere & skybox"
		guilist [
			guilist [genskyitems $skies1]
			guibar

			guilist [
				loop h 4 [
					guibutton (strstatecolor (= $_curskyoption $h) (at ["atmosphere" "clouds" "fog" "lighting"] $h) "4" "8") [_curskyoption = @h] (at ["action" "arrow_fw"] (= $_curskyoption $h))
				]
				guistrut 13 1
			]
			guilist [
				atmooptions $_curskyoption
			]
		]

		guitab "^f8config"
		guitext (mapcfgname)
		showfileeditor (mapcfgname) -70 15
	]
] "^f8editing"

// atmooptions function //
_skyboxeslist = (getfileslist "packages/skyboxes" "png")
_curlighttype = 1
atmooptions = [
	guistayopen [
		if (= $arg1 0) [
			guitext "^f8atmosphere"
			guicheckbox (strstatecolor $atmo "show atmosphere") atmo
			guilistsplit atmf 3 [atmoalpha atmobright atmodensity atmohaze atmoheight atmoozone atmoplanetsize atmosundiskbright atmosundiskcorona atmosundisksize atmosunlightscale] [
				guialign 0 [guitext (strreplace (strreplace $atmf "atmo" "") "sun" "") 0]
				guilist [guinumfield $atmf 5 (at [0.05 0.5] (>= $$atmf 10)) [] "+fround" 5]
			]
			guialign 0 [guitext "atmo light type" 0]
			guialign 0 [
				guiradio (strstatecolor (= $_curlighttype 0) "sunlight") _curlighttype 0
				guistrut 0.5
				guiradio (strstatecolor (= $_curlighttype 1) "sundisk") _curlighttype 1
			]
			guicolorslider (at [atmosunlight atmosundisk] $_curlighttype) 0 255 []
		]
		if (= $arg1 1) [
			guitext "^f8clouds"
			_ct = 0
			guilist [
				guilistsplit curcloud 1 $_skyboxeslist [
					guiimage (concatword "<thumbnail:32>packages/skyboxes/" $curcloud ".png") [cloudlayer (concatword "skyboxes/" @curcloud)] 1 0 "data/cube.png"
				]
			]
			guilistsplit cldf 3 [cloudalpha cloudclip cloudfade cloudheight cloudoffsetx cloudoffsety cloudscale cloudscrollx cloudscrolly] [
				guialign 0 [guitext (strreplace $cldf "cloud" "") 0]
				guilist [guinumfield $cldf 5 (at [0.05 0.5] (>= $$cldf 10)) [] "+fround" 5]
			]
			guicolorslider cloudcolour 0 255 []
		]
		if (= $arg1 2) [
			guitext "^f8fog"
			guilist [
				guicheckbox (strstatecolor $fogdomecap "domecap") fogdomecap
				guicheckbox (strstatecolor $fogdomeclouds "domeclouds") fogdomeclouds
			]

			guilistsplit fgdf 3 [fogdomeclip fogdomeheight fogdomemax fogdomemin] [
				guialign 0 [guitext (strreplace $fgdf "fog" "") 0]
				guilist [guinumfield $fgdf 5 (at [0.05 0.5] (>= $$fgdf 10)) [fog $foglevel] "+fround" 5]
			]
			guialign 0 [
				guilist [
					guialign 0 [guitext "fog distance" 0]
					guilist [guinumfield fog 8 16 []]
				]
			]
			guicolorslider fogcolour 0 255 []
		]
		if (= $arg1 3) [
			guilist [
				guitext "^f8lighting"
				guicheckbox (strstatecolor $fullbright "fullbright") fullbright
				guistrut 4.15
					
			]
			guilist [
				guibutton "calclight^t" [calclight]
				guibutton "patch" [patchlight]
			]
			guilist [
				guibutton "remip^t" [remip]
				guibutton "recalc" [recalc]
			]
			guialign 0 [
				guilist [
					guialign 0 [guitext (strstatecolor (= (indexof [512 32 16 1] $lightprecision) -1) "lightprecision" ~ 2) 0]
					guilist [
						guiradio (strstatecolor (= $lightprecision 512) "fast" 4 0) lightprecision 512
						guiradio (strstatecolor (= $lightprecision 32) "default" 4 1) lightprecision 32
						guiradio (strstatecolor (= $lightprecision 16) "pretty" 4 6) lightprecision 16
					]
					guialign 0 [guilist [guilist [guinumfield lightprecision 5 15 []]]]
				]
			]
			guialign 0 [
				looplist lgdf [lighterror lightthreads] [
					guilist [
						guialign 0 [guitext $lgdf 0]
						guialign 0 [guilist [guilist [guinumfield $lgdf 3 1 []]]]
					]
				]
			]
			guialign 0 [guitext "light type" 0]
			guialign 0 [
				guiradio (strstatecolor (= $_curlighttype 0) "skylight") _curlighttype 0
				guiradio (strstatecolor (= $_curlighttype 1) "sunlight") _curlighttype 1
			]
			guicolorslider (at [skylight sunlight] $_curlighttype) 0 255 []
			if (= $_curlighttype 1) [
				guibutton "getsundir" [getsundir]
			]
		]
	]
]

quickeditmenu = [
	local curmap
	curmap = (? (=s $mapname "") "untitled" $mapname)
	guilist [
		guilist [
			guialign -1 [
				guilist [
					guialign 0 [
						guitext "save map" 0
					]
					guifield savemap_name 10 [savemap $savemap_name]
				]
			]

			guibutton "quick light" [calclight -1]
			guibutton "optimize map" [remip]
			guibutton "new entity" [newent shells]
			guibutton "new map" [newmap]
			guibutton help [showgui editing]
		]
		guibar
		guilist [
			guiimage (concatword "<thumbnail: 256>packages/base/" $curmap ".jpg") [echo (concatword "packages/base/" $curmap ".jpg")] 4 0 "packages/textures/nomapthumb.png"
		]
		guilist [
			guibutton "" [cleargui; savemapthumb "packages/base" $curmap [genentgui]] "icons8/camera.png"
			guibutton "" [delmapgui (? (=s $mapname "") "untitled" $mapname) [genentgui]] "trash.png"
			guibutton "" [_ctxmap = $mapname; showcustommaps] "plus.png"
		]
	]

]

matmenu = [
	looplist mat ["air" "water" "lava" "clip" "glass" "noclip" "gameclip" "death" "alpha"] [guibutton $mat [editmat @mat]]
	guibitfield "show material volumes" showmat 1
]

brushmenu = [
	guilist [ // diamonds
		guiimage "packages/icons/brush_1c.png" brush_0
		guiimage "packages/icons/brush_21c.png" brush_1
		guiimage "packages/icons/brush_421c.png" brush_2
	]
	guilist [ // squares
		guiimage "packages/icons/brush_3s.png" brush_3
		guiimage "packages/icons/brush_5s.png" brush_4
		guiimage "packages/icons/brush_7s.png" brush_5
	]
	guititle "smooth"
	guilist [ // smooth
		guiimage "packages/icons/brush_3s.png" brush_6
		guiimage "packages/icons/brush_5s.png" brush_7
		guiimage "packages/icons/brush_7s.png" brush_8
	]
]

newgui materials [
	@matmenu
	guitab "^f8misc"
	@quickeditmenu
] "^f8materials"

newgui brushes [
	@brushmenu
	guitab "^f8misc"
	@quickeditmenu
] "^f8brushes"

newgui quickedit [
	@quickeditmenu
	guitab "^f8materials"
	@matmenu
] "^f8quickedit"


newgui seltexinfo [
	guitext (concatword "Slot: " (getseltex))
	guilist [
		loop x 5 [
			texname = (gettexname (getseltex) $x)
			texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
			guiimage (concatword "packages/" $texname) "" 1 1
		]
	]
	loop x 5 [tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
]

contexteditgui = [
	if $hmapedit [showgui brushes] [
		if (enthavesel) [
			showgui (enttype)
		] [showgui (? (havesel) materials quickedit)]
	]
]
