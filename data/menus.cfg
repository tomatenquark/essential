// standard menu definitions
// don't modify, add personal menus to autoexec.cfg instead

togglemainmenu = [
    if (! (cleargui 1)) [
        showgui main
    ]
]

bind ESCAPE [togglemainmenu]

selplayermodel = [guirolloveraction = (playermodelbutton $playermodel); showgui playermodel]
newgui main [
    guilist [
    	guiimage (concatword "packages/icons/" (playermodelicon) ".jpg") [selplayermodel] 1.1
        guistrut 0.25
        guilist [
            newname = (getname)
            guifield newname 15 [name $newname]
            guispring
            guilist [
                guibutton (concatword "^f8" (playermodelname)) [selplayermodel] 0
                guistrut 1
                guiimage (getcrosshair) [showgui crosshair] 0.5
            ]
        ]
    ]
    guibar
    guibutton "server browser"  [showgui "servers"] "server_browser.png"

    if (isconnected) [
        guibar
        guibutton "vote game mode / map" "showgui gamemode"
        // guibutton "lanconnect"
        // guibutton "connect localhost"

        if (&& (m_teammode (getmode)) (isconnected)) [
            guibutton (concat "switch to team" (at ["3evil" "1good"] (indexof ["good" "evil"] (getteam)))) [if (strcmp (getteam) "good") [team evil] [team good]]
        ]
        guibutton "toggle spectator" [
            spectator (! (isspectator (getclientnum)))
            if (&& (! (haspriv (getclientnum))) (isspectator (getclientnum))) [if (>= (getmastermode) 2) [echo (concatword "^f3could not spawn, server in " (at ["open" "veto" "locked" "private"] (getmastermode)) " mode")]]
        ] "spectator"
        guibutton "master" [showgui "master"]
        if (= (getclientnum) -1)[
            guibutton "demo editor" [showgui "demomanage"]
        ]
        guibutton "options" [showgui "options"] "gear.png"
        guibar
        guibutton "disconnect" [disconnect] "exit"
        if (|| $editing (m_edit (getmode))) [
            guitab "^f8editing"
            guitext "save current map:"
            guifield savemap_name 20 [savemap $savemap_name]
            guibutton "^f2show saved maps" [showcustommaps] "icons8/folder.png"
            guibutton "newmap (default: 12)" [newmap $newmapsize]
            guislider "newmapsize" 10 16
            guibutton "increase mapsize (2x)" [mapenlarge]
            guitext "undo size (default: 5 MB)"
            guislider "undomegs" 1 10
            guibutton "optimize geometry" [remip]
            guibutton "set map title/author" [saycommand "/maptitle "]
            guibutton "^f2advanced editing" [showgui "editing"]
        ]
    ] [
        guibutton "bot match" [showgui "botmatch"] "npc.png"
        if (! $isconnected)[guibutton "map editor" [newmap; edittoggle; outline 1; echo "^f7Default editing keys:^n^t^f1E^f2: Switch between edit mode and test mode^n^t^f1F2^f2: Textures ^f7/ ^f1F3^f2: Entities ^f7/ ^f1F4^f2: Objects ^f7/ ^f1F5^f2: Save map ^f7/ ^f1F7^f2: Advanced editing menu"] "modes/editing.png"]
        guibar
        guibutton "options" [showgui "options"] "gear.png"
        guilist [
            guibutton "about" [showgui "about"]
            guistrut 1
            guibutton "help" [addzip "data/guide.zip"; exec "guide.cfg"; showgui guide]
        ]
        guibutton "quit" [
            if (=s (getalias quitconfirm) "")[quitconfirm = 1]
            if (quitconfirm) [
                newgui "quit" [
                    guitext "are you sure you want to quit?"
                    guialign 0 [
                        guibutton "return" [cleargui 1] "arrow_bw"
                        guibar
                        guibutton "quit" [quit] "exit"
                    ]
                    guicheckbox "always ask" quitconfirm
                ] 0
                showgui "quit"
            ] [quit]
        ] "exit"
    ]
] "^f8main"

newgui about [
    guitext "^f6Tomatenquark ^f~is a fork of ^f6Cube 2: Sauerbraten"
    guitext "brought by the Tomatenquark Team, based on the"
    guitext "efforts of ^f8Wouter ^"Aardappel^" van Oortmerssen^f~,"
    guitext "^f8Lee ^"eihrul^" Salzman ^f~and many more."
    guilist [guitext "(for a full list of contributors "; guibutton "^f1see the credits" [showgui credits] 0; guitext ")" 0]
    guitext "^f5https://tomatenquark.org/"
    guitext "^f5http://sauerbraten.org/"
    guitext "^f5http://cubeengine.com/"
] "^f8about"

macro playermodelbutton [cleargui 1; playermodel %1]

newgui playermodel [
    pmidx = (substr $guirolloveraction (strlen (playermodelbutton "")))
    pmidx = (max 0 (min (- $playermodelnum 1) $pmidx))
    guinoautotab [
        guilist [
            guilist [
                loop i $playermodelnum [
                    guibutton (playermodelname $i) (playermodelbutton $i) (playermodelicon $i)
                ]
                guiplayerpreview $pmidx 0 6 $guirolloveraction 4.5 0
            ]
            guibar
            guilist [
                guilist [
                    guitextbox (playermodelstory $pmidx) 40 9
                ]
                guilist [
                    guicheckbox "force matching player models" forceplayermodels
                    guibar
                    guicheckbox "always use team skins" teamskins
                ]
                guilist [
                    guicheckbox "fullbright" fullbrightmodels 75 0
                    if $fullbrightmodels [
                        guibar
                        guiradio "subtle" fullbrightmodels 75
                        guiradio "bright" fullbrightmodels 100
                        guiradio "overbright" fullbrightmodels 150
                        guiradio "max" fullbrightmodels 200
                    ]
                ]
            ]

        ]
    ]
] "^f8player model"

crosshairs = ["data/crosshair.png data/hit.png"]
findcrosshairs "packages/crosshairs"
findcrosshairs "packages/crosshairs/tomatenquark"
findcrosshairs "packages/crosshairs/freepik"
findcrosshairs "packages/crosshairs/mx"
findcrosshairs "packages/crosshairs/1brunobruce"

intcrosshairsize = 15
newgui crosshair [
    guitext "crosshair size"
    guislider intcrosshairsize 0 50 [crosshairsize $intcrosshairsize]
    loop row (div (+ (listlen $crosshairs) 6) 7) [
        guilist [
            loop col 7 [
                ch = (at $crosshairs (+ (* $row 7) $col))
                if (!=s $ch "") [
                    guiimage (at $ch 0) [
                        cleargui 1
                        loadcrosshair @(at $ch 0) 0
                        loadcrosshair @(at $ch (if (> (listlen $ch) 1) 1 0)) 2
                        echo (concat "^f7new crosshair set:" @(at $ch 0))
                    ] 0.5
                ]
            ]
        ]
    ]
] "^f8crosshair"

mmclient = [
    guiitemmove -1 -1 -0.5 [
        guilist [
            guibar
            guibutton (concatword (format "^f%1(%2)^f7" (at ["5" "4"] (= (getfollow) @cn)) (getclientnum @cn)) (at (getclientpriv @cn) 0) (getclientname @cn) (at [@(result "[^t^t]") @(result "[^t]")] (= (strlen (getclientname @cn)) 15))) [follow @@cn] (getclienticon @cn)
        ]
        guiitemmove 0 0 -1 [
            guilist [if (haspriv (getclientnum)) [guitext (tabify "" 5) 0][guitext (tabify "" 6) 0]; guibar]
        ]
    ]
    guiitemmove -1 0 -1.5 [guitext (tabify "" $mmtbs) 0; guibar]
    if (haspriv (getclientnum)) [
        guiitemmove -1 21 -1.5 [
            guilist [
                guibutton "kick " [kick @@cn]
                guibar
                guibutton (at [@(result "[spec  ]") @(result "[unspec]")] (isspectator @cn)) [spectator @@(= (isspectator @@@cn) 0) @@cn]
                if (m_teammode (getmode)) [
                    guibar
                    guibutton "switch team " [setteam @@cn @@(? (=s (getclientteam @@@cn) "good") "evil" "good")]
                ]
                guibar
                if (ismaster @cn) [
                    guibutton "take master" [setmaster 0 @@cn]
                ] [
                    guiitemmove 0 0 0.15 [
                        guibutton "give master" [setmaster 1 @@cn]
                    ]
                ]
                if (m_teammode (getmode))[guiitemmove 0 0 1.87 [guitext "" 0; guibar]] [guiitemmove 0 0 0.55 [guitext "" 0; guibar]]
            ]
        ]
    ]
]

_splitspec = 0
newgui master [
    if (! $isconnected)[
        guialign 0 [guitext "^f3you have been disconnected, kicked or banned."]
        guibutton "server browser.." [showgui "servers"]
        guibutton "bot match.." [showgui "botmatch"]
        guibar
        guibutton "reconnect" [reconnect]
        guibutton "main" [cleargui] "arrow_bw"
    ] [
        guistayopen [
        guilist [guistrut 60]        
        guiitemmove 1 0 -1.7 [
            if (&& (isconnected) (!=s $connectedip "")) [
                guitext (concatword $connectedip ":" $connectedport) 0
            ]
        ]
        guialign 1 [
            if (ismaster (getclientnum)) [
                guibutton "relinquish master" [setmaster 0]
            ] [
                guibutton "claim master" [if (getalias _privpass)[setmaster $_privpass][setmaster 1]]
            ]
        ]
        guiitemmove -1 0 -1 [guialign -1 [guitext "^f7password:^t" 0; guifield _privpass -10]]
        guiitemmove 0 0 -1 [
            guitext (concatword (at (getclientpriv (getclientnum)) 0) (at ["no privileges" "master" "admin" "auth"] (at (getclientpriv (getclientnum)) 1))) 0
        ]
        mm = (getmastermode)
        mmlist = ["open" "veto" "locked" "private"]
        mmdesc = [
            "any client can join and claim master privileges if available."
            "2all clients are able to vote for a map."
            "6when joining, the client is forced to spectate."
            "3other clients are unable to join without a server password or auth."
        ]

        guialign 0 [
            guititle "^t"
            looplist m $mmlist [
                guiradio (format "^f7(%1%2^f7)%1%3" (at [ @(result (at ["4" "7"] (|| $isadmin $ismaster))) @(result "^f8")] (=s $mm (indexof $mmlist $m))) (indexof $mmlist $m) $m) mm (indexof $mmlist $m) [if (! $setmaster)[setmaster 1]; mastermode (indexof $mmlist @m)]
            ]
        ]
        guiitemmove 0 8 0 [guitextbox (at $mmdesc $mm) 38 2 "0xAAFFFF"]
        guibar
        guiitemmove -1 0 -0.5 [
            if (haspriv (getclientnum)) [
                guialign -1 [
                    guibutton (at [ @(result "[1pause game^t]") @(result "[6unpause game^t]")] $paused) [if (= $paused 1) [pausegame 0][pausegame 1]]
                    guibar
                    guibutton "clearbans^t" [clearbans]
                    guibar
                    guibutton "record demo" [recorddemo 1]
                ]
            ]
            if (|| $editing (m_edit (getmode))) [
                guialign -1 [
                    guibutton "start newmap^t" [if (= $editing 0)[edittoggle; savemap temp; newmap; edittoggle][newmap]]
                    guibar
                    guibutton "show custom maps.." [showcustommaps]
                ]
                guialign -1 [
                    guibutton "savemap^t^t" [savemap]
                    guibar
                    guibutton "sendmap^t" [sendmap]
                    guibar
                    guibutton "getmap" [getmap]
                ]
            ]
            if (! (haspriv (getclientnum)))[guistrut 1]
        ]

        if (> (listlen (listclients 0 0)) 0) [
            //Players
            guitab (concatword "^f8players^f7(" (at [@(result (listlen (listclients 0 0))) @(result (at (numplayers) 0))] $_splitspec) ")")
            if (haspriv (getclientnum)) [if (m_teammode (getmode)) [mmtbs = 17] [mmtbs = 13]] [mmtbs = 6]

            if (> (at (numplayers) 0) 0) [
                guitext (concatword "^f8playing^f7: " (at (numplayers) 0)) 0
                if (haspriv (getclientnum))[
                    guiitemmove 1 0 -1 [guibutton "specall" [looplist c (listclients 0 0) [spectator 1 $c]] "spectator"]
                ]
                guiitemmove -1 0 -1.4 [guitext (tabify "" $mmtbs) 0; guibar]
                looplist cn (listclients 0 0) [
                    if (! (isspectator $cn))[@@mmclient]
                ]
            ] [guialign 0 [guitext "^f7no active players"]; guibar]

            //Spectators
            if (> (at (numplayers) 1) 0) [
                if (_splitspec)[guitab (concatword "^f8spectators^f7(" (at (numplayers) 1) ")") ]

                if (|| (haspriv (getclientnum)) (= $_splitspec 1) (= (at (numplayers) 0) 0)) [
                    guitext (concatword "^f8watching^f7: " (at (numplayers) 1)) 0
                    guiitemmove -1 10 -1 [guicheckbox "split tabs" _splitspec]
                    if (haspriv (getclientnum))[
                        guiitemmove 1 0 -1 [guibutton "unspecall" [looplist c (listclients 0 0) [spectator 0 $c]] "spectator"]
                    ]
                    guiitemmove -1 0 -1.4 [guitext (tabify "" $mmtbs) 0; guibar]
                    looplist cn (listclients 0 0) [
                        if (isspectator $cn)[@@mmclient]
                    ]
                ] [
                    guiitemmove 1 0 (concatword "-" (+ (at (numplayers) 0) 1) ".638") [
                        guitext (concatword "^f8watching^f7: " (at (numplayers) 1)) 0
                        guiitemmove -1 10 -1 [guicheckbox "split tabs" _splitspec]
                        guiitemmove -1 0 -1.4 [guitext (tabify "" $mmtbs) 0; guibar]
                        looplist cn (listclients 0 0) [
                            if (isspectator $cn)[@@mmclient]
                        ]
                    ]
                ]
            ]
        ]
    ]
    ]
] "^f7server"

botmatchcount = 5
botmatchminskill = 50
botmatchmaxskill = 85

startbotmatch = [
    if (isconnected 0 0) [
        echo "You must disconnect from the current multiplayer game before starting a bot match."
    ] [
        loop i $botmatchcount [addbot (rnd (+ $botmatchmaxskill 1) $botmatchminskill)]
    ]
]

newgui botmatch [
    guitext "bot amount"
    guislider botmatchcount 0 32
    guitext "bot minimum skill"
    guislider botmatchminskill 50 101 [if (< $botmatchmaxskill $botmatchminskill) [botmatchmaxskill = $botmatchminskill]]
    guitext "bot maximum skill"
    guislider botmatchmaxskill 50 101 [if (> $botmatchminskill $botmatchmaxskill) [botmatchminskill = $botmatchmaxskill]]
    guibutton "start match.." [guionclear [startbotmatch]; showgui gamemode]
] "^f8bot match"

newgui _lanserver [
    guistayopen [
        guialign -1 [
            guitext "server name:^t"
            guifield serverdesc -10
            guibar
            guitext "server port:^t"
            guifield serverport -10
        ]
        guialign -1 [
            guitext "^f6admin ^f~pass:^t"
            guifield adminpass -10
            guibar
            guitext "max clients:^t"
            guifield maxclients -10
        ]
        guialign -1 [
            guicheckbox "public server" publicserver
        ]
        guibar
        guialign 0 [
            guibutton "return" [cleargui 1] "arrow_bw"
            guibar
            guitext "LAN:^t"
            guibutton "^f1start" [startlistenserver] 0
            guitext " / " 0
            guibutton "^f3stop" [stoplistenserver] 0
            guitext " / " 0
            guibutton "^f0connect" [lanconnect] 0
        ]
    ]
] 0

newgui servers [
    guistayopen [
        guiservers [
            guilist [
                guibutton "update from master server" [updatefrommaster]
                guibar
                guicheckbox "search LAN" [searchlan]
                guibar
                guicheckbox "auto-update" [autoupdateservers]
                guibar
                guicheckbox "auto-sort" [autosortservers]
                if (= $autosortservers 0) [
                    guibar
                    guibutton "sort" [sortservers]
                ]
                guibar
                guibutton "reconnect" [reconnect]
                guibar
                guibutton "LAN server" [showgui _lanserver] "ping.png"
            ]
            guibar
        ] 17
    ]
] "^f8servers" [initservers]

showfileeditor = [
    guinoautotab [
        guieditor $arg1 $arg2 $arg3
        textinit $arg1 $arg1
        guistayopen [
            guilist [
                guibutton "load" [textfocus @arg1; textload @arg1]
                guibar
                guibutton "save" [textfocus @arg1; textsave @arg1]
                guibar
                guibutton "exec" [textfocus @arg1; textexec]
                guibar
                guibutton "copy" [textfocus @arg1; textcopy]
                guibar
                guibutton "paste" [textfocus @arg1; textpaste]
                guibar
                guibutton "select" [textfocus @arg1; textselectall]
                guibar
                guibutton "clear" [textfocus @arg1; textclear]
            ]
        ]
    ]
]

notepadfile = "untitled.txt"

newgui notepad [
    guifield notepadfile -30
    showfileeditor $notepadfile -80 20
] "^f8notepad"

notepad = [
    if (> $numargs 0) [notepadfile = $arg1]
    showgui notepad
]

newgui pastebuffer [
    guinoautotab [
        guieditor "#pastebuffer" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#pastebuffer"; textexec]
                guibar
                guibutton "clear" [textfocus "#pastebuffer"; textclear]
            ]
        ]
    ]
]

pastebuffer = [showgui pastebuffer]

newgui scratchpad [
    guinoautotab [
        guieditor "#scratchpad" -80 20
        guistayopen [
            guilist [
                guibutton "exec" [textfocus "#scratchpad"; textexec]
                guibar
                guibutton "copy" [textfocus "#scratchpad"; textcopy]
                guibar
                guibutton "paste" [textfocus "#scratchpad"; textpaste]
                guibar
                guibutton "select" [textfocus "#scratchpad"; textselectall]
                guibar
                guibutton "clear" [textfocus "#scratchpad"; textclear]
            ]
        ]
    ]
]

scratchpad = [showgui scratchpad]

newmapsize = 12
savemap_name = temp
_curskyoption = 0
newgui editing [
    guilist [
        guilist [
            guitext "^f8command"
            guibutton "materials / entity" [genentgui (entget)]
            guibutton "toggle edit mode" [edittoggle]
            guitext "toggle heightmap"
            guibutton "deselect" [cancelsel]
            //guibutton "passthrough" [passthrough]
            guibutton "copy" [copy]
            guibutton "paste" [paste]
            guibutton "flip / mirror cube" [flip]
            guibutton "undo" [undo]
            guibutton "redo" [redo]
            guibutton "delete" [editdel]
            guibutton "texture palette" [showtexgui]
            //guibutton "edit entity gui" [showentgui]
            guibutton "mapmodels list" [showgui mapmodels]
            guibutton "select all in box" [entselect insel]
            guibutton "select all matching" [selentfindall]
            //guibutton "center view on ent " [entautoview]
            guibutton "edit entity console" [selentedit]
            guicheckbox "toggle outline" [outline]
            guicheckbox "toggle wireframe" [wireframe]
        ]
        guilist [
            guitext "^f8shortcut"
            looplist sc ["F3" "E" "H" "SPACE" "C" "V" "X" "Z, U" "I" "BACKSPACE" "F2" "F4" "keypad ENTER" "N" "PERIOD" "7" "8"] [
                guitext (concatword "^f2(" $sc ")") 0
            ]
        ]
        guibar
        guilist [
            guitext "^f8mouse action"
            guitext "push/pull selection"
            guitext "push the face of a cube "
            guitext "change gridsize"
            guitext "change textures"
            guitext "rotate"
            guitext "push corner with cursor"
            guitext "push/pull heightmap"
            guitext "change heightmap brush"
            guitext "toggle heightmap tex"
            guitext "select"
            guitext "select corners"
            guitext "extend selection"
            guitext "reorient"
            guitext "move selection"
        ]
        guilist [
            guitext "^f8shortcut"
            looplist sc ["scroll only" "hold F + scroll" "hold G + scroll" "hold Y + scroll" "hold R + scroll" "hold Q + scroll" "hold LALT + scroll" "hold B + scroll" "MMB in HM mode" "drag left mouse" "drag middle mouse" "right mouse" "right mouse" "drag right mouse"] [
                guitext (concatword "^f2(" $sc ")") 0
            ]
        ]
    ]
    guitab "^f8entity"
    guilist [
      guilist [
        guibutton "drop entities.."              "showgui dropent"
        guibutton "mapmodels.."                  "showgui mapmodels"
        guicheckbox "mapmodel warnings"          "mapmodelwarnings"
        guibutton "light.."                      "resetlight;showgui newlight"

        guibutton "newent playerstart   "
        guibutton "newent shells"
        guibutton "newent bullets"
        guibutton "newent rockets"
        guibutton "newent riflerounds"
        guibutton "newent grenades"
      ]
      guilist [
        guibutton "teleport.."                   "showgui newteleport"
        guibutton "monster.."                    "showgui monsters"
        guibutton "trigger.."                    "showgui triggers"

        guibutton "newent cartridges"
        guibutton "newent health"
        guibutton "newent healthboost"
        guibutton "newent greenarmour"
        guibutton "newent yellowarmour"
        guibutton "newent quaddamage"
      ]
    ]
    //guitab "^f8map lighting"
    //guibutton "calclight 1 (slow: 8xAA)" "calclight 1"
    //guibutton "calclight -1 (quick: no AA, no model shadows)"   "calclight -1"
    //guibutton "patchlight"                         "patchlight"
    //guicheckbox "fullbright" fullbright
    //guitext "lightprecision (default: 32)"
    //guilistslider lightprecision "8 16 32 48 64 128 256"
    //guitext "lighterror (default: 8)"
    //guislider lighterror
    //guitext "lightthreads (CPU threads/cores) (default: 0)"
    //guislider lightthreads

    guitab "^f8texture blend"
    guitext "blend paint mode:"
	guiradio "off						(keypad 0)" blendpaintmode 0
	guiradio "replace					(keypad 1)" blendpaintmode 1
	guiradio "dig						(keypad 2)" blendpaintmode 2
	guiradio "fill						(keypad 3)" blendpaintmode 3
	guiradio "inverted dig				(keypad 4)" blendpaintmode 4
	guiradio "inverted fill				(keypad 5)" blendpaintmode 5
    guibar
    guitext "paint blendmap			(MOUSE1)"
	guibutton "change blend brush			scroll" "nextblendbrush"
	guibutton "rotate blend brush			(MOUSE2)" "rotateblendbrush"
	guibutton "clear entire blendmap 		(^"clearblendmap^" command)" "clearblendmap"
	guibutton "clear blendmap in selection	(^"clearblendmapsel^" command)" "clearblendmapsel"

    guitab "^f8atmosphere & skybox"
    guistayopen [      
        guilist [  
            guilist [genskyitems $skies1]
            guibar
            guilist [atmooptions $_curskyoption]
            guistrut 2
            guilist [
                loop h 4 [
                    guibutton (strstatecolor (= $_curskyoption $h) (at ["atmosphere" "clouds" "fog" "lighting"] $h) "4" "8") [_curskyoption = @h] (at ["action" "arrow_fw"] (= $_curskyoption $h))
                ]
            ]
        ]
    ]

    guitab "^f8config"
    guitext (mapcfgname)
    showfileeditor (mapcfgname) -70 15
] "^f8editing"


skies1 = [
skyboxes/cruz/amazon
skyboxes/cruz/antartic
skyboxes/cruz/ethna
skyboxes/cruz/myth
skyboxes/cruz/rockwell
skyboxes/cruz/rune
skyboxes/djtex/isle
skyboxes/djtex/isless
skyboxes/djtex/mountains
skyboxes/penguins/arid
skyboxes/penguins/barren
skyboxes/penguins/divine
skyboxes/penguins/gloom
skyboxes/penguins/harmony
skyboxes/penguins/wrath
skyboxes/penguins/yonder
skyboxes/penguins/zeus
skyboxes/lordkv/kvstar1K
]

genskyitems = [
    //looplist cursky $arg1 [
    //    guibutton $cursky (concat skybox $cursky) //"cube"
    //]
    guilistsplit cursky 4 $arg1 [showskyshot $cursky (concat skybox $cursky)]
]

showskyshot = [
    _curskythumb = (concatword (if (> $numargs 0) [result $arg1] [result (at $guirollovername 0)]) "_ft.");
    //guibar
    guiimage (concatword "<thumbnail:32>packages/" $_curskythumb (at ["jpg" "png"] (findfile (concatword "packages/" $_curskythumb "png")) )) $arg2 1 1 "data/cube.png"
]


newgui materials [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"

    guicheckbox "show material volumes"    showmat

    //guibutton "waterlod very high		(3)"  "waterlod 3"
    //guibutton "waterlod high		(2)"          "waterlod 2"
    //guibutton "waterlod default		(1)"  "waterlod 1"
    //guibutton "waterlod low		(0)"          "waterlod 0"

    //guibutton "watersubdiv very fine	(0)"          "watersubdiv 0"
    //guibutton "watersubdiv fine		(1)"  "watersubdiv 1"
    //guibutton "watersubdiv default		(2)"  "watersubdiv 2"
    //guibutton "watersubdiv coarse		(3)"  "watersubdiv 3"

    //guibutton "water waves on"                     "waterwaves 1"
    //guibutton "water waves off"                    "waterwaves 0"
]

setting_entediting = 1

newgui dropent [
    guitext "drop entities:"
    guiradio "to the floor"                 entdrop 1
    guiradio "on selection"                 entdrop 2
    guiradio "to the floor at selection"    entdrop 3
    guiradio "at current position"          entdrop 0
    guibar
    guicheckbox "entity snap-to-grid"       entselsnap
    guicheckbox "entity selection"          setting_entediting 1 0 [ entediting $setting_entediting ]
]

newgui triggers [
    guibutton [invisible]               [BTmm = -1; showgui trigger2]
    guibutton [a carrot]                [BTmm = 23; showgui trigger2]
    guibutton [switch #1]               [BTmm = 24; showgui trigger2]
    guibutton [switch #2]               [BTmm = 25; showgui trigger2]
    guibutton [door #1]                 [BTmm = 26; showgui trigger2]
    guibutton [door #2]                 [BTmm = 27; showgui trigger2]
    guibutton [door #3]                 [BTmm = 28; showgui trigger2]
]

newgui trigger2 [
    guibutton [animation loop]                      [BTtt = 0; showgui trigger3]
    guibutton [trigger once (popback)]              [BTtt = 1; showgui trigger3]
    guibutton [trigger once (popback & rumble)]     [BTtt = 2; showgui trigger3]
    guibutton [trigger once]                            [BTtt = 3; showgui trigger3]
    guibutton [trigger once (rumble)]               [BTtt = 4; showgui trigger3]
    guibutton [trigger multiple (popback)]          [BTtt = 5; showgui trigger3]
    guibutton [trigger multiple (popback & rumble)] [BTtt = 6; showgui trigger3]
    guibutton [trigger multiple]                        [BTtt = 7; showgui trigger3]
    guibutton [trigger multiple (rumble)]           [BTtt = 8; showgui trigger3]
    guibutton [door (open once)]                        [BTtt = 9; showgui trigger3]
    guibutton [door (auto close)]                   [BTtt = 10; showgui trigger3]
    guibutton [door (locked)]                       [BTtt = 11; showgui trigger3]
    guibutton [vanishing trigger]                   [BTtt = 12; showgui trigger3]
    guibutton [vanishing trigger (rumble)]          [BTtt = 13; showgui trigger3]
    guibutton [End Of Level Trigger]                    [BTtt = 29; showgui trigger3]
]

build_trigger = [newent mapmodel $BTmm $BTtt $BTti]

newgui trigger3 [
    loop i 9 [
        tjt = (concat tag (concatword # $i))
        tjc = (concat BTti "=" $i ";" "build_trigger")
        guibutton $tjt $tjc
    ]
]


resetlight = [
    lightcolour = 0
    lightbright = 1
    lightset 255 255 255
    lightradius = 128
]

lightset = [
    lightr = $arg1
    lightg = $arg2
    lightb = $arg3
    if (= $lightbright 0) lightscale
]

lightscale = [
    if (= $lightbright 0) [
        lightr = (div $lightr 2); lightg = (div $lightg 2); lightb = (div $lightb 2)
    ] [
        lightr = (min 255 (* $lightr 2)); lightg = (min 255 (* $lightg 2)); lightb = (min 255 (* $lightb 2))
    ]
]

lightcmd = [
    result (concat newent light $lightradius $lightr $lightg $lightb)
]

newgui newlight [
    guibutton "sunlight"    "newent light 0 255 255 255"
    guibutton (lightcmd)
    guibar
    guitext "color:"
    guicolor (+ (* (+ (* $lightr 256) $lightg) 256) $lightb)
    guislider lightr 0 255
    guislider lightg 0 255
    guislider lightb 0 255
    guilist [
        guicheckbox "bright"    lightbright 1 0 [lightscale]
        guibar
        guiradio "white"        lightcolour 0 [lightset 255 255 255]
        guiradio "blue"         lightcolour 1 [lightset 192 192 255]
        guiradio "red"          lightcolour 2 [lightset 255 192 192]
        guiradio "green"        lightcolour 3 [lightset 192 255 192]
        guiradio "yellow"       lightcolour 4 [lightset 255 255 192]
        guiradio "purple"       lightcolour 5 [lightset 255 192 255]
        guiradio "turquoise"    lightcolour 6 [lightset 192 255 255]
    ]
    guitext "radius:"
    guislider lightradius 0 512
]

newgui newteleport [
    guibutton "newent teleport 1"
    guibutton "newent teledest 1"
    guibutton "newent teleport 2"
    guibutton "newent teledest 2"
    guibutton "newent teleport 3"
    guibutton "newent teledest 3"
    guibutton "newent teleport 4"
    guibutton "newent teledest 4"
]

_mmtab = 0
_mmloaded = ""
_mmtimer = 0
newgui mapmodels [
    guistayopen [
        if (!= (nummapmodels) 0) [
            local Xn Yn XYn mmid mmsel
            Xn = 7
            Yn = 4
            XYn = (* $Xn $Yn)

            guilist [
                guispring
                loop "n" (ceil (divf $nummapmodels $XYn)) [
                    if $n [ guibar ]
                    guilist [
                        guistrut 3.5 1
                        if (&& [! $guiclicktab] [=s (concatword " ^f7" (+ $n 1) " ") $guirollovername]) [ _mmtab = $n ]
                        guialign 0 [
                            guibutton (format " %1%2 " (? (= $_mmtab $n) "^f8" "^f7") (+ $n 1)) [ _mmtab = @n ] 0
                        ]
                    ]
                ]
                guispring
            ]
            guistrut 0.4
            loop "y" $Yn [
                guilist [
                    loop "x" $Xn [
                        mmid = (+ (+ (* $XYn $_mmtab) (* $y $Xn)) $x)
                        if (< $mmid $nummapmodels) [
                            mmsel = (= (at $guirolloverimgaction 2) $mmid)
                            guiimage "<mad:0.1/0.1/0.1>packages/skyboxes/plain/white_ft.png" [ newent mapmodel @mmid; genentgui (entget) ] 3 0
                            guiitemmove -1 0 -12 [ guimodelpreview (mapmodelname (? (< (indexof $_mmloaded $mmid) 0) -1 $mmid)) "mapmodel" [] 3 0 ]
                            if (!= (listlen $_mmloaded) $nummapmodels) [ // merely there for added efficiency
                                if (&& [< (indexof $_mmloaded $mmid) 0] [> (- $getmillis $_mmtimer) 50]) [
                                    append _mmloaded $mmid
                                    _mmtimer = $getmillis
                                ]
                            ]
                            guiitemmove -1 0.2 -11.3 [ guititle (concatword (? $mmsel "^f8" "^f4") $mmid) ]
                        ] [ guistrut 6 ; guistrut 6 1 ]
                    ]
                ]
            ]
            guistrut 0.2
            guitext (concatword "^f7" (strreplace (mapmodelname (at $guirolloverimgaction 2)) "/" "^f4/^f7")) 0
            guiitemmove 1 0 -1 [
                guilist [
                    guibutton "^f1list mode" [showgui mapmodels2] "arrow_bw"
                    guibar
                    guibutton "^f3reset mapmodels" [mapmodelreset; allmapmodels = ""] "trash.png"
                ]
            ]
        ] [
            guitextbox "No mapmodel has been registered for this map. This usually happens after ^f8/mapmodelreset^f~." 29 3
            guibutton "^f1register default mapmodels" [exec "data/default_map_models.cfg"]
        ]
    ]
] 0

_curtabmmidx1 = 0
_curtabmmidx2 = 0
_curhovermm = -1
_genmapmodelslist = [
    allloadedmapmodels = (getmapmodels 0 "result")
    _curhovermmb = (getstrbetween (strreform (at $guirollovername 0)) "[" "]" 1)
    if (isnumber $_curhovermmb) [_curhovermm = $_curhovermmb]
    guilist [
        guilist [
            guiitemmove -1 -30 0 [
                guimodelpreview (mapmodelname $_curhovermm) "mapmodel" [entcancel; newent mapmodel $_curhovermm] 7 0
            ]
        ]
        
        guilist [
            guilistscroll mmidx@arg2 $arg1 [
                mmname = (mapmodelname $mmidx@arg2)
                if (= (strstr $mmname "mapmodels") 0) [mmname = (substr $mmname 10)]
                if (> (strlen $mmname) 25) [mmname = (strscroll $mmname 25 300 0)]
                guilist [
                    guistrut 1
                    guimodelpreview (mapmodelname $mmidx@@arg2) "mapmodel" [entcancel; newent mapmodel @@mmidx@@@arg2] 0.5 1
                    guibutton (concatword "^f8[" $mmidx@@arg2 "]^t^f7" (strstatecolor (= $_curhovermm $mmidx@@arg2) $mmname "7" "0")) [newent mapmodel @@mmidx@@@arg2; genentgui (entget); entautoview] 0
                ]
                guistrut 30 1
            ] 14
        ]
    ]
]

newgui mapmodels2 [
    guistayopen [
        if (nummapmodels) [
            guilist [
                if (!= $_curtabmmidx1 (- (nummapmodels) 14)) [guibutton "jump to last" [_curtabmmidx1 = (- (nummapmodels) 14)]] [guibutton "jump to first" [_curtabmmidx1 = 0]]
                if (!= $_curhovermm "-1") [guistrut 1; guibutton "^f7reload model" [clearmodel (mapmodelname $_curhovermm)]]
            ]
            _genmapmodelslist (getmapmodels 1 "result") 1
            guialign 1 [
                guibutton "^f1grid mode" [showgui mapmodels] "arrow_fw"
            ]
            guialign 1 [guibutton "^f3reset mapmodels" [mapmodelreset; allmapmodels = ""] "trash.png"]
            if (listlen $allloadedmapmodels) [
                guitab (concatword "^f8Loaded (^f7" (listlen $allloadedmapmodels) "^f8)")
                _genmapmodelslist $allloadedmapmodels 2
            ]
        ] [
            showgui mapmodels
        ]
    ]
] "^f8Registered"


newgui monsters [
    guibutton "ogro / fireball"      "newent monster 0"
    guibutton "rhino / chaingun"      "newent monster 1"
    guibutton "ratamahatta / shotgun"      "newent monster 2"
    guibutton "slith / rifle" "newent monster 3"
    guibutton "bauul / RL"     "newent monster 4"
    guibutton "hellpig / bite"       "newent monster 5"
    guibutton "knight / iceball" "newent monster 6"
    guibutton "goblin / slimeball"   "newent monster 7"
    guibutton "spider / grenade"    "newent monster 8"
] "^f8monsters"

newgui postfx [
    guibutton "(effect OFF)"          "clearpostfx"
    guibutton "bloom (subtle: 30%)"    "bloom 0.3"
    guibutton "bloom (bright: 55%)"    "bloom 0.55"
    guibutton "bloom (intense: 80%)"  "bloom 0.8"
    guibutton "rotoscope" "rotoscope 1"
    guibutton "rotoscope + blur3" "rotoscope 1 1"
    guibutton "rotoscope + blur5" "rotoscope 1 2"
    guibutton "sobel"  "setpostfx sobel"
    guibutton "invert" "setpostfx invert"
    guibutton "gbr"    "setpostfx gbr"
    guibutton "bw"     "setpostfx bw"
    guibutton "blur3"  "setpostfx hblur3; addpostfx vblur3"
    guibutton "blur5"  "setpostfx hblur5; addpostfx vblur5"
]

macro resbutton [
    guibutton "%1x%2" "screenres %1 %2" (if (&& (= $scr_w %1) (= $scr_h %2)) [result "radio_on"] [result "radio_off"])
]

_gameoptions = 0
newgui options [
    guistrut 64 1
    guilist [
        guiradio (concatword "^f" (at ["1" "8" "8"] $_gameoptions) "general") _gameoptions 0
        guibar
        guiradio (concatword "^f" (at ["8" "1" "8"] $_gameoptions) "graphics") _gameoptions 1
        guibar
        guiradio (concatword "^f" (at ["8" "8" "1"] $_gameoptions) "HUD") _gameoptions 2
    ]
    if (= $_gameoptions 0) [
        guibar
        guicheckbox "blood^t^t" blood
        guicheckbox "hit sound" hitsound
        guicheckbox "show client numbers" showclientnum
        guicheckbox "show quit confirmation" quitconfirm
        guicheckbox "show scoreboard at death" deathscore
        guilist [
            guicheckbox "damage screen^t"     damagescreen
            guicheckbox "damage compass"    damagecompass
        ]
        guilist [
            //guicheckbox "outline meters^t" outlinemeters
            guicheckbox "numbered bases^t" basenumbers
            guicheckbox "radar teammates" radarteammates 1
        ]
        guilist [
            guiradio "single map backup^t" savebak 1
            guiradio "backup every save" savebak 2
        ]

        guilist [
            guicheckbox "crosshair effects^t" crosshairfx
            if $crosshairfx [
                guicheckbox "colors^t" healthcolors
                guicheckbox "teammates^t" teamcrosshair
                guicheckbox "hits" hitcrosshair 425
            ]
        ]

        guilist [
            guicheckbox "hide dead players^t" hidedead 1 0
            if (|| (= $hidedead 0) (= $hidedead 2)) [
                guicheckbox "ragdoll^t" ragdoll
                guicheckbox "keep body^t" ragdollmillis 10000
                guicheckbox "hide all except yourself" hidedead 2 0
            ]
        ]

        guicheckbox "invert mouse"      invmouse
        guilist [
            guitext "mouse sensitivity: " "mouse.png"
            newsens = $sensitivity
            guitext "game " 0
            guifield newsens 8 [sensitivity $newsens]
            newguisens = $guisens
            guitext " interface " 0
            guifield newguisens 8 [guisens $newguisens]
        ]
        guibar
        guibutton "^f3restore defaults (clears ALL settings)" "exec restore.cfg"
    ]
    if (= $_gameoptions 1) [
        guistrut 0.2
        guialign 0 [
            guistayopen [
                guibutton "^f0fast^t" [graphicquality 0]
                guibutton "^f2moderate^t" [graphicquality 1]
                guibutton "^f3slow and pretty" [graphicquality 2]
            ]
        ]
        guistrut 0.2
        guilist [
            guitext "shaders^t^t"
            guiradio "^f0low detail^t^t" shaderdetail 0
            guiradio "^f2medium detail^t" shaderdetail 2
            guiradio "^f3high detail" shaderdetail 3
        ]
        guilist [
            guitext "water^t^t"
            guicheckbox "^f2refraction^t^t"  waterrefract
            guicheckbox "^f3reflection^t^t"  waterreflect
            guicheckbox "^f0caustics^t"    caustics
            guicheckbox "^f0animation" vertwater
        ]
        guilist [
            guitext "waterfalls^t^t"
            guicheckbox "^f2refraction^t^t" waterfallrefract
            guicheckbox "^f0reflection" waterfallenv
        ]
        guilist [
            guicheckbox "^f3soft shadows^t"  shadowmap
            if $shadowmap [
                guiradio "^f0low detail^t^t" shadowmapsize 7 [blurshadowmap 1]
                guiradio "^f2medium detail^t" shadowmapsize 9 [blurshadowmap 1]
                guiradio "^f3high detail" shadowmapsize 11 [blurshadowmap 0]
            ] [
                guicheckbox "^f0blob shadows" blobs
            ]
        ]
        if $glare [
            glarepreset = 0
            if (= $glarescale 1) [
                if (= $blurglare 4) [glarepreset = 1]
                if (= $blurglare 7) [glarepreset = 3]
            ]
            if (= $glarescale 2) [
                if (= $blurglare 3) [glarepreset = 2]
                if (= $blurglare 7) [glarepreset = 4]
            ]
            guilist [
                guicheckbox "^f3glare^t^t^t"       glare
                guiradio "^f2subtle^t^t" glarepreset 1 [blurglare 4; glarescale 1]
                guiradio "^f2glowy^t^t" glarepreset 2 [blurglare 3; glarescale 2]
                guiradio "^f3soft^t^t" glarepreset 3 [blurglare 7; glarescale 1]
                guiradio "^f3intense" glarepreset 4 [blurglare 7; glarescale 2]
            ]
        ] [
            guilist [
                guicheckbox "^f3glare^t^t^t"         glare
            ]
        ]
        guilist [
            guicheckbox "^f3motion blur^t" motionblur
            if $motionblur [
                guiradio "^f3subtle^t^t" motionblurscale 0.5
                guiradio "^f3moderate^t^t" motionblurscale 0.65
                guiradio "^f3intense" motionblurscale 0.8
            ]
            
        ]
        guilist [
            guicheckbox "^f3grass^t^t" grass
            if $grass [
                guiradio "^f2quick fade^t" grassdist 128
                guiradio "^f2moderate fade^t" grassdist 256
                guiradio "^f3slow fade" grassdist 512
            ]
        ]
        guilist [
            guicheckbox "^f0dynamic lights^t"    maxdynlights 3 0
            if $maxdynlights [
                guiradio "^f0medium detail^t" maxdynlights 3
                guiradio "^f2high detail" maxdynlights 5
            ]
        ]
        guilist [
            guicheckbox "^f0soft particles^t" depthfx
            if $depthfx [
                guiradio "^f0low detail^t^t" depthfxsize 7 [blurdepthfx 1]
                guiradio "^f2medium detail^t" depthfxsize 10 [blurdepthfx 0]
                guiradio "^f3high detail" depthfxsize 12 [blurdepthfx 0]
            ]
        ]
        guilist [
            guicheckbox "^f0glass reflection^t"  glassenv
        ]
        guilist [
            guicheckbox "^f0decals^t^t" decals
            if $decals [
                guiradio "^f0quick fade^t" decalfade 10000 [maxdecaltris 1024]
                guiradio "^f2slow fade" decalfade 60000 [maxdecaltris 4096]
            ]
        ]
        guicheckbox "^f0fix t-joints^t" filltjoints
        guilist [
            guitext "textures^t^t"
            guiradio "^f0low detail^t^t" maxtexsize 256
            guiradio "^f0medium detail^t" maxtexsize 512
            guiradio "^f2high detail" maxtexsize 0
        ]
        guilist [
            guitext "models^t^t"
            guicheckbox "^f0lighting^t^t"    lightmodels
            guicheckbox "^f0reflection^t^t"  envmapmodels
            guicheckbox "^f0glow^t^t"        glowmodels
            guicheckbox "^f2bumpmap" bumpmodels
        ]
    ]
    if (= $_gameoptions 2) [hudoptions]
    guitab    "^f8display"
    guicheckbox "v-sync"            vsync 1 0
    guilist [
        guicheckbox "fullscreen " [fullscreen]
        guistayopen [guibutton "toggle and adjust" [togglefullscreen]]
    ]
    guitext "gamma (default: 100)"
    guislider gamma
    guitext "full-scene anti-aliasing (default: -1)"
    guilistslider fsaa "-1 0 2 4 8 16"
    guitext "z-buffer depth (default: 0)"
    guilistslider depthbits "0 16 24 32"
    guitext "anisotropic filtering (default: 0)"
    guilistslider aniso "0 2 4 8 16"
	guilist [
    	guicheckbox "bilinear filtering" bilinear
		guibar
		guicheckbox "trilinear filtering (mipmaps)" trilinear
	]

    guitab   "^f8resolution"
    guitext "horizontal field of view (default: 107)"
    guislider fov
    guistayopen [
      guilist [
        guilist [
            guitext "4:3"
            @@@@(resbutton 320 240)
            @@@@(resbutton 640 480)
            @@@@(resbutton 800 600)
            @@@@(resbutton 1024 768)
            @@@@(resbutton 1152 864)
            @@@@(resbutton 1280 960)
            @@@@(resbutton 1400 1050)
            @@@@(resbutton 1600 1200)
            @@@@(resbutton 1792 1344)
            @@@@(resbutton 1856 1392)
            @@@@(resbutton 1920 1440)
            @@@@(resbutton 2048 1536)
            @@@@(resbutton 2800 2100)
            @@@@(resbutton 3200 2400)
        ]
        guibar
        guilist [
            guitext "16:10"
            @@@@(resbutton 320 200)
            @@@@(resbutton 640 400)
            @@@@(resbutton 1024 640)
            @@@@(resbutton 1280 800)
            @@@@(resbutton 1440 900)
            @@@@(resbutton 1600 1000)
            @@@@(resbutton 1680 1050)
            @@@@(resbutton 1920 1200)
            @@@@(resbutton 2048 1280)
            @@@@(resbutton 2560 1600)
            @@@@(resbutton 2880 1800)
            @@@@(resbutton 3200 2000)
            @@@@(resbutton 3840 2400)
        ]
        guibar
        guilist [
            guitext "16:9"
            @@@@(resbutton 1024 600)
            @@@@(resbutton 1280 720)
            @@@@(resbutton 1366 768)
            @@@@(resbutton 1600 900)
            @@@@(resbutton 1920 1080)
            @@@@(resbutton 2048 1152)
            @@@@(resbutton 2560 1440)
            @@@@(resbutton 2880 1620)
            @@@@(resbutton 3200 1800)
            @@@@(resbutton 3840 2160)
        ]
        guibar
        guilist [
            guitext "5:4"
            @@@@(resbutton 600 480)
            @@@@(resbutton 1280 1024)
            @@@@(resbutton 1600 1280)
            @@@@(resbutton 2560 2048)
        ]
        guibar
        guilist [
            guitext "5:3"
            @@@@(resbutton 800 480)
            @@@@(resbutton 1280 768)

            guibar
            guitext "Custom"
            guilist [
                customw = $scr_w
                customh = $scr_h
                guifield customw 4 [scr_w $customw]
                guifield customh 4 [scr_h $customh]
            ]
        ]
      ]
    ]

    guitab   "^f8sound"
    guitext "master volume" "sound.png"
    guislider soundvol
    guitext "music volume" "sound.png"
    guislider musicvol
    guitext "sound channels" "sound.png"
    guislider soundchans
    guitext "sound frequency" "sound.png"
    guilistslider soundfreq "11025 22050 44100"
    guitext "sound buffer length" "sound.png"
    guislider soundbufferlen
    guicheckbox "Mumble positional audio" mumble

    guitab "^f8keys"
    _cursearchb = (at [searchbinds searcheditbinds searchspecbinds] $_keytype)
    _curkeyb = (at [bind editbind specbind] $_keytype)
    guitext (concatword "(for anything more use the '^f1/" $_curkeyb " key [action]^f~' command)")    
    guitext "select action to bind and press desired keys (^f1ESC ^f~when done):^t"
    guialign 0 [
        guiradio "bind" _keytype 0
        guiradio "editbind" _keytype 1
        guiradio "specbind" _keytype 2
        guicheckbox "debug" dbgbind
    ]
    guilistsplit n 2 $(at [bindsactions editbindsactions specbindsactions] $_keytype) [
        guilist [
            guitext (tabify (format "^f1%1: " (if (!=s (at $n 1) "") [result (at $n 1)] [result (at $n 0)])) 4) (at $n 2)
            n = (at $n 0)
            [newbinds@i] = (@_cursearchb $n)
            guikeyfield [newbinds@i] -9 [
                oldbinds = (@_cursearchb [@@n])
                looplist j $oldbinds [@_curkeyb $j ""; echo (concatword "^f8" @_curkeyb " ^f1" $j " ^f7removed")]
                looplist j $[newbinds@@i] [@_curkeyb $j [@@@n]; echo (concatword "^f7new ^f8" @_curkeyb "^f7: ^f1" $j "^f~ [^f8" @@n "^f~]")]
            ]
        ]
    ] [guibar]

    guitab "^f8console"
    guicheckbox "chat console" miniconfilter 0x300 0
    guitext "console size (lines)"
    guislider consize
    guitext "console filter:"
    guilist [
        guilist [
            guibitfield "important information" confilter 0x01
            guibitfield "warnings" confilter 0x02
            guibitfield "errors" confilter 0x04
            guibitfield "init messages" confilter 0x10
            guibitfield "script messages" confilter 0x20
	    ]
        guibar
        guilist [
            guibitfield "chat" confilter 0x100
            guibitfield "team chat" confilter 0x200
            guibitfield "game events" confilter 0x400
            guibitfield "self frags" confilter 0x800
            guibitfield "other player frags" confilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "team kills" confilter 0x2000
        ]
    ]
    guitext "full console size (percent of screen)"
    guislider fullconsize
    guitext "full console filter:"
    guilist [
        guilist [
            guibitfield "important information" fullconfilter 0x01
            guibitfield "warnings" fullconfilter 0x02
            guibitfield "errors" fullconfilter 0x04
            guibitfield "init messages" fullconfilter 0x10
            guibitfield "script messages" fullconfilter 0x20
        ]
        guibar
        guilist [
            guibitfield "chat" fullconfilter 0x100
            guibitfield "team chat" fullconfilter 0x200
            guibitfield "game events" fullconfilter 0x400
            guibitfield "self frags" fullconfilter 0x800
            guibitfield "other player frags" fullconfilter 0x1000
        ]
        guibar
        guilist [
            guibitfield "team kills" fullconfilter 0x2000
        ]
    ]

    guitab "^f8autoexec.cfg"
    guitext "autoexec.cfg ^f7- ^f8this file runs automatically when starting"
    showfileeditor "autoexec.cfg" -60 15
] "^f8game"

bindsactions = [forward backward left right jump ["attack" "shoot"] togglezoom saycommand sayteamcommand showscores toggleconsole [screenshot "" "icons8/camera.png"] ["edittoggle" "" "modes/editing.png"] fastsaw togglefullscreen ["setweapon FI" "chainsaw" "modes/fist.png"] ["setweapon SG" "shotgun" "modes/shotg.png"] ["setweapon CG" "chaingun" "modes/chaing.png"] ["setweapon RL" "rocket launcher" "modes/rocket.png"] ["setweapon RI" "rifle" "modes/rifle.png"] ["setweapon GL" "grenade launcher" "modes/gl.png"] ["setweapon PI" "pistol" "modes/pistol.png"] "weapon" ["universaldelta 1" "scroll up"] ["universaldelta -1" "scroll down"] dropflag addbot delbot]
editbindsactions = [[holdhidehud "hide hud"] editflip editcopy editpaste cancelsel floatspeedboost showtexgui calclight selentfindall gotosel ["domodifier 1" "grid size"] ["domodifier 2" "push face"] ["domodifier 3" "push corner"] ["domodifier 4" "rotate sel"] ["domodifier 6" "textures pallete"] ["domodifier 9" "heightmap brush"] ["domodifier 11" "entity attr0"] ["domodifier 12" "entity attr1"] ["domodifier 13" "entity attr2"] ["domodifier 14" "entity attr3"] ["domodifier 15" "entity attr4"] ["domodifier 16" "texture offset H"] ["domodifier 17" "texture offset V"]]
specbindsactions = [follow nextfollow ["nextfollow -1" "previous follow"] floatspeedboost togglezoom]
entupdate = [ entset $entguitype $entguiattr0 $entguiattr1 $entguiattr2 $entguiattr3 $entguiattr4 ]

initentgui = [
  entguitype = (enttype)
  @(loopconcat i 5 [result [
    entguiattr@i = (entattr @i)
  ]])
]

genentattributes = [
    entattributes = (loopconcat i (listlen $arg2) [
        entattribname = (at $arg2 $i)
        entattriblimits = (at $arg3 (* 2 $i))
        entattriblimits2 = (at $arg3 (+ 1 (* 2 $i)))
        if (=s (at $entattriblimits 0) $entattriblimits) [
            result [
                guitext @entattribname
                guislider entguiattr@i @entattriblimits @entattriblimits2 entupdate
            ]
        ] [
            result [
                guitext @entattribname
                guinameslider entguiattr@i [@@entattriblimits] [@@entattriblimits2] entupdate
            ]
        ]
    ])
]

guilistsplit = [
  guilist [
    i = 0
    l = (listlen $arg3)
    z = (div (+ $l (- $arg2 1)) $arg2)
    loop a $arg2 [
      guilist [
        t = (min (+ $i $z) $l)
        while [< $i $t] [
          $arg1 = (at $arg3 $i)
          arg4
          i = (+ $i 1)
        ]
      ]
      if (&& (>= $numargs 5) (< (+ $a 1) $arg2)) [arg5]
    ]
  ]
]

quickeditmenu = [
  guitext "Quick Commands:"
  guibar
  guifield  savemap_name 10 [ savemap $savemap_name ]
  guibutton "quick light"       "calclight -1"
  guibutton "optimize map"      "remip"
  guibutton "new entity"        "newent shells"
  guibar
  guibutton newmap
  guibar
  guibutton help "showgui editing"
]

matmenu = [
    guibutton "air"                        "editmat air"
    guibutton "water"                      "editmat water"
    guibutton "lava"                       "editmat lava"
    guibutton "clip"                       "editmat clip"
    guibutton "glass"                      "editmat glass"
    guibutton "noclip"                     "editmat noclip"
    guibutton "gameclip"                   "editmat gameclip"
    guibutton "death"                      "editmat death"
    guibutton "alpha"                      "editmat alpha"
    guicheckbox "show material volumes"    showmat
]

brushmenu = [
  guilist [ // diamonds
  guiimage "packages/icons/brush_1c.png" brush_0
  guiimage "packages/icons/brush_21c.png" brush_1
  guiimage "packages/icons/brush_421c.png" brush_2
  ]
  guilist [ // squares
  guiimage "packages/icons/brush_3s.png" brush_3
  guiimage "packages/icons/brush_5s.png" brush_4
  guiimage "packages/icons/brush_7s.png" brush_5
  ]
  guititle smooth
  guilist [ // smooth
  guiimage "packages/icons/brush_3s.png" brush_6
  guiimage "packages/icons/brush_5s.png" brush_7
  guiimage "packages/icons/brush_7s.png" brush_8
  ]
]

newentgui = [
  genentattributes $arg1 $arg2 $arg3
  newgui $arg1 [
    guitext $entguitype
    guibar
    @entattributes
    guitab type
    guilistsplit n 2 $enttypelist [
        guibutton $n [ entset @n ]
    ]
    guitab "^f8misc"
    @quickeditmenu
  ] "" [initentgui]
]

looplist i $enttypelist [
  newentgui $i "" ""
]

newgui materials [
  @matmenu
  guitab "^f8misc"
  @quickeditmenu
] "^f8materials"

newgui brushes [
  @brushmenu
  guitab "^f8misc"
  @quickeditmenu
] "^f8brushes"

newgui quickedit [
  @quickeditmenu
  guitab "^f8materials"
  @matmenu
] "^f8quickedit"

newentgui light "radius red green blue" "0 400 0 255 0 255 0 255"
newentgui spotlight "radius" "0 200"
newentgui playerstart "direction" "0 360"
newentgui teleport "tag" "0 20"
newentgui teledest "direction tag" "0 360 0 20"
newentgui monster "direction type" "0 360 0 7"
newentgui mapmodel "direction model" "0 360 0 100"
newentgui envmap "radius" "0 400"
newentgui jumppad "Z Y X" "0 200 0 200 0 200"
newentgui sound "type radius size" "0 20 0 500 0 500"
newentgui particles "type" "0 3"

contexteditgui = [
    if $hmapedit [showgui brushes] [
        if (enthavesel) [
            showgui (enttype)
        ] [showgui (? (havesel) materials quickedit)]
    ]
]

showentgui = [ contexteditgui ] // legacy bind

newgui seltexinfo [
    guitext (concatword "Slot: " (getseltex))
    guilist [ loop x 5 [
        texname = (gettexname (getseltex) $x)
        texname = (substr $texname (+ (strstr $texname ">") 1)) // chop off any leading commands
        guiimage (concatword "packages/" $texname) "" 1 1
    ]]
    loop x 5 [ tex = (gettexname (getseltex) $x); if (strcmp $tex "") [] [guitext $tex]]
]

//List of contributors (in alphabetical order)
//Criteria: Contributed and somehow interacted with Tomatenquark
_tq_contributors = [
"Calinou" "Fatality" "Fixxxer" "Fohlen"
"Gangler" "koppiman" "Markus 'makkE' Bekel" "Mod"
"NopNotMe" "Obstriegel" "Ognjen 'Svarog' Robovic" "Ogrobastard"
"Q009" "Quinton Reeves" "RaZgRiZ" "Redon"
"Salatiel" "Siesta" "Taurustar" "ZackXevious" "<tqlogo>"
]
//Criteria: Is in the list of used licenses but has not interacted with Tomatenquark
_tq_contributors1 = [
"Alexander 'pix' Willing" "cruZ" "Djakk" "crivain"
"Georges 'TRaK' Grondin" "jdagenet" "Jean Ayers" "Jerimee"
"John 'geartrooper' Siar" "Kai Engel" "Kurtis Kesler" "lukv"
"Markus Druffel" "Lunaran" "Mike 'MitaMAN' Poeschl" "Nieb"
"PainKillAH" "Philip Klevestav" "Psionic" "qubodup"
"Sheik" "Simon 'Sock' O'Callaghan" "SkiingPenguins" "Tavmjong Bah"
"Tomek" "W. Sitters" "Xeno" "Yves Allaire"
"<tqlogo>"
]
//Criteria: Is in the list of used licenses but does not have a specific author
_tq_contributors2 = [
"Golgotha Project" "icons8.com"
"Inexor Team" "Loopix Project"
"Ouwlish Media" "Redeclipse Team"
]
_tq_contributorstitle = [
    [guitextbox "Who directly contributed to ^f6Tomatenquark" 17 0 0x00afb3]
    [guitextbox "Who contributed to other projects, but is included" 17 0 0x00afb3]
    [guitextbox "Other projects that made content available" 16 0 0x00afb3]
]

newgui credits [
    guistayopen [
        guiimage "data/background.png" [echo "^f8Thank you all!"] 13 0
        guiitemmove 0 0 -20 [
            guiitemmove 0 0 -5 [guiimage "data/cube.png" [] 3 0]
            guitextbox "These people kindly helped with code, content or expenses." 50 0
            guilist [
                loop _clist 3 [
                    _curclist = $(at [_tq_contributors _tq_contributors1 _tq_contributors2] $_clist)
                    _curnamecolor = 0
                    guilist [
                        do (at $_tq_contributorstitle $_clist)
                        looplist _tqc1 (listscroll $_curclist 8 1500 0) [
                            if (=s $_tqc1 "<tqlogo>") [
                                guiitemmove 0 0 -1 [guitext "^t^t^t^t" 0; guibar]
                                guiitemmove 0 0 -1 [guitext "" "tq.png"]
                            ] [
                                if (> (strlen $_tqc1) 15) [_curcname = (strscroll $_tqc1 15 150 4)] [_curcname = $_tqc1]
                                guialign 0 [guitextbox $_curcname 15 0 (at [0x2e4e69 0x3a6386 0x44759e 0xFFFFFF 0x4f8bbd 0x44759e 0x3a6386 0x2e4e69] $_curnamecolor)]
                            ]
                            _curnamecolor = (+ $_curnamecolor 1)
                        ] 4
                    ]
                ]
            ]
            guitext "" 0
            guitext "^f4For a complete list of used licenses, visit tomatenquark.org" 0
        ]
    ]
] 0
credits = [showgui credits]

if (! (findfile "config.cfg")) [addzip "data/guide.zip"; exec "guide.cfg"; showgui guide]
